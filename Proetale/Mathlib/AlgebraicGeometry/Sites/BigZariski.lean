import Mathlib.AlgebraicGeometry.Sites.BigZariski
import Mathlib.AlgebraicGeometry.Limits
import Mathlib.CategoryTheory.Sites.Preserves
import Proetale.Mathlib.CategoryTheory.Limits.Preserves.Finite
import Proetale.Mathlib.AlgebraicGeometry.Sites.MorphismProperty

universe u

open CategoryTheory Limits Opposite

namespace CategoryTheory

@[simp]
lemma GrothendieckTopology.pullback_hom_mem_iff {C : Type*} [Category C] {J : GrothendieckTopology C}
    {X Y : C} {e : X â‰… Y} {S : Sieve Y} :
    S.pullback e.hom âˆˆ J X â†” S âˆˆ J Y := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ J.pullback_stable e.hom hâŸ©
  simpa using J.pullback_stable e.inv h

end CategoryTheory

namespace AlgebraicGeometry

/--
The big Zariski site on the category of all `u`-schemes has sheafification in `Type u`.
This requires a refactor of sheafification in mathlib.
-/
proof_wanted hasSheafify_zariskiTopology : HasSheafify Scheme.zariskiTopology.{u} (Type u)

lemma preservesFiniteProducts_of_isSheaf_zariskiTopology {F : Scheme.{u}áµ’áµ– â¥¤ Type*}
    (hF : Presieve.IsSheaf Scheme.zariskiTopology F) :
    PreservesFiniteProducts F := by
  apply Limits.preservesFiniteProducts_of_preservesLimitsOfShape
  intro Î¹ _
  apply (config := { allowSynthFailures := true }) preservesLimitsOfShape_of_discrete
  intro X
  let X' := unop âˆ˜ X
  show PreservesLimit (Discrete.functor fun i â†¦ op (X' i)) F
  have (i : Î¹) : Mono (Cofan.inj (Sigma.cocone (Discrete.functor X')) i) :=
    inferInstanceAs <| Mono (Sigma.Î¹ _ _)
  refine Presieve.preservesProduct_of_isSheafFor F ?_ initialIsInitial
      (Sigma.cocone (Discrete.functor X')) (coproductIsCoproduct' _) ?_ ?_
  Â· apply hF.isSheafFor
    convert (âŠ¥_ Scheme).bot_mem_grothendieckTopology
    rw [eq_bot_iff]
    rintro Y f âŸ¨g, _, _, âŸ¨iâŸ©, _âŸ©
    exact i.elim
  Â· intro i j hij
    refine âŸ¨?_, âŸ¨?_âŸ©âŸ©
    Â· simp
    Â· refine PullbackCone.IsLimit.mk ?_ ?_ ?_ ?_ ?_
      Â· intro s
        have : IsEmpty s.pt := isEmpty_of_commSq_sigmaÎ¹_of_ne âŸ¨s.conditionâŸ© hij
        exact isInitialOfIsEmpty.to _
      Â· intro s
        have : IsEmpty s.pt := isEmpty_of_commSq_sigmaÎ¹_of_ne âŸ¨s.conditionâŸ© hij
        apply isInitialOfIsEmpty.hom_ext
      Â· intro s
        have : IsEmpty s.pt := isEmpty_of_commSq_sigmaÎ¹_of_ne âŸ¨s.conditionâŸ© hij
        apply isInitialOfIsEmpty.hom_ext
      Â· intro s m
        have : IsEmpty s.pt := isEmpty_of_commSq_sigmaÎ¹_of_ne âŸ¨s.conditionâŸ© hij
        intro x y
        apply isInitialOfIsEmpty.hom_ext
  Â· exact hF.isSheafFor _ _ (sigmaOpenCover X').generate_ofArrows_mem_grothendieckTopology

lemma Scheme.IsLocallyDirected.ofArrows_mem_grothendieckTopology {J : Type*} [Category J]
    (F : J â¥¤ Scheme.{u})
    [âˆ€ {i j : J} (f : i âŸ¶ j), IsOpenImmersion (F.map f)] [(F.comp Scheme.forget).IsLocallyDirected]
    [Quiver.IsThin J] [Small.{u} J] :
    Sieve.ofArrows _ (colimit.Î¹ F) âˆˆ Scheme.zariskiTopology (colimit F) :=
  (Scheme.IsLocallyDirected.openCover F).generate_ofArrows_mem_grothendieckTopology

lemma ofArrows_Î¹_mem_zariskiTopology_of_isColimit {Î¹ : Type*} {F : Discrete Î¹ â¥¤ Scheme.{u}}
    [Small.{u} Î¹] (c : Cocone F) (hc : IsColimit c) :
    Sieve.ofArrows _ c.Î¹.app âˆˆ Scheme.zariskiTopology c.pt := by
  let iso : c.pt â‰… colimit F := hc.coconePointUniqueUpToIso (colimit.isColimit F)
  rw [â† GrothendieckTopology.pullback_hom_mem_iff (e := iso.symm)]
  apply GrothendieckTopology.superset_covering _ ?_ ?_
  Â· exact Sieve.ofArrows _ (colimit.Î¹ F)
  Â· rw [Sieve.ofArrows, Sieve.generate_le_iff]
    rintro - - âŸ¨iâŸ©
    exact âŸ¨_, ðŸ™ _, c.Î¹.app i, âŸ¨iâŸ©, by simp [iso]âŸ©
  Â· exact Scheme.IsLocallyDirected.ofArrows_mem_grothendieckTopology F

end AlgebraicGeometry
