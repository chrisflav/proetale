/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib
import Mathlib.CategoryTheory.Sites.LeftExact
import Mathlib.AlgebraicGeometry.Limits
import Mathlib.CategoryTheory.Limits.Shapes.Countable
import Proetale.Mathlib.CategoryTheory.Limits.Shapes.DisjointCoproduct
import Proetale.Mathlib.AlgebraicGeometry.Sites.BigZariski
import Proetale.Mathlib.CategoryTheory.Sites.Canonical
import Mathlib.CategoryTheory.Limits.Preserves.Ulift

universe v u

open CategoryTheory Limits

namespace CategoryTheory

variable {C : Type*} [Category C] {A B : Type*} [Category A] [Category B]
  (J : GrothendieckTopology C) (F : A â¥¤ B) [J.HasSheafCompose F]

instance (K : Type*) [Category K] [PreservesLimitsOfShape K F] [HasLimitsOfShape K A] :
    PreservesLimitsOfShape K (sheafCompose J F) :=
  have : PreservesLimitsOfShape K (sheafCompose J F â‹™ sheafToPresheaf J B) :=
    inferInstanceAs <| PreservesLimitsOfShape K
      (sheafToPresheaf J A â‹™ (Functor.whiskeringRight Cáµ’áµ– A B).obj F)
  CategoryTheory.Limits.preservesLimitsOfShape_of_reflects_of_preserves _
    (sheafToPresheaf J B)

@[simp]
lemma Presieve.isSheafFor_comp_uliftFunctor_iff {F : Cáµ’áµ– â¥¤ Type u} {X : C} {R : Presieve X} :
    R.IsSheafFor (F â‹™ uliftFunctor.{v}) â†” R.IsSheafFor F := by
  rw [Presieve.IsSheafFor, Presieve.IsSheafFor]
  refine âŸ¨fun h x hx â†¦ ?_, fun h x hx â†¦ ?_âŸ©
  Â· let x' : R.FamilyOfElements (F â‹™ uliftFunctor.{v}) := fun Y f hf â†¦ âŸ¨x f hfâŸ©
    have hx' : x'.Compatible := by
      introv Yâ‚ h
      simp [x', hx gâ‚ gâ‚‚ hâ‚ hâ‚‚ h]
    obtain âŸ¨t, ht, huniqâŸ© := h _ hx'
    refine âŸ¨t.down, ?_, ?_âŸ©
    Â· intro Y g hg
      exact Equiv.ulift.symm.injective (ht g hg)
    Â· intro t' ht'
      exact Equiv.ulift.symm.injective (huniq _ fun Y g hg â†¦ Equiv.ulift.injective (ht' g hg))
  Â· let x' : R.FamilyOfElements F := fun Y f hf â†¦ (x f hf).down
    have hx' : x'.Compatible := by
      introv Yâ‚ h
      simpa [x'] using hx gâ‚ gâ‚‚ hâ‚ hâ‚‚ h
    obtain âŸ¨t, ht, huniqâŸ© := h _ hx'
    refine âŸ¨âŸ¨tâŸ©, ?_, ?_âŸ©
    Â· intro Y g hg
      exact Equiv.ulift.injective (ht g hg)
    Â· intro t' ht'
      exact Equiv.ulift.injective (huniq _ fun Y g hg â†¦ Equiv.ulift.symm.injective (ht' g hg))

@[simp]
lemma Presieve.isSheaf_comp_uliftFunctor_iff {F : Cáµ’áµ– â¥¤ Type u} :
    IsSheaf J (F â‹™ uliftFunctor.{v}) â†” IsSheaf J F := by
  simp [IsSheaf, Presieve.isSheafFor_comp_uliftFunctor_iff]

end CategoryTheory

namespace AlgebraicGeometry

noncomputable
instance : CoproductsDisjoint Scheme.{u} where
  CoproductDisjoint X Y := by
    have : Mono (BinaryCofan.mk (coprod.inl (X := X) (Y := Y)) coprod.inr).inl := by
      dsimp
      infer_instance
    have : Mono (BinaryCofan.mk (coprod.inl (X := X) (Y := Y)) coprod.inr).inr := by
      dsimp
      infer_instance
    refine CoproductDisjoint.of_binaryCofan_of_pullbackCone (.mk coprod.inl coprod.inr) ?_ ?_ ?_ ?_
    Â· exact coprodIsCoprod X Y
    Â· exact pullback.cone _ _
    Â· exact pullback.isLimit _ _
    Â· apply Nonempty.some
      rw [isInitial_iff_isEmpty]
      exact isEmpty_of_commSq_sigmaÎ¹_of_ne.{0, u} (Î¹ := WalkingPair) (X := fun j â†¦ j.casesOn X Y)
        (i := .left) (j := .right) âŸ¨pullback.conditionâŸ© (by simp)

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
instance : HasSheafify Scheme.zariskiTopology.{u} (Type (u + 1)) := inferInstance

instance : MonoCoprod Scheme.{u} := by
  refine .mk' fun X Y â†¦ âŸ¨BinaryCofan.mk coprod.inl coprod.inr, coprodIsCoprod X Y, ?_âŸ©
  dsimp
  infer_instance

attribute [local instance] Types.instFunLike Types.instConcreteCategory in
/-- One possible proof is to embed into the category of Zariski sheaves. -/
instance : FinitaryExtensive Scheme.{u} := by
  let C := ULiftHom.{u + 1, u + 1} Scheme
  let e : Scheme.{u} â‰Œ C := ULiftHom.equiv
  let J : GrothendieckTopology C :=
    e.inverse.inducedTopology Scheme.zariskiTopology
  have : J.Subcanonical := by
    refine .of_isSheaf_yoneda_obj _ fun X â†¦ ?_
    show Presieve.IsSheaf J ((e.inverse.op â‹™ yoneda.obj X) â‹™ uliftFunctor.{u + 1, u})
    rw [Presieve.isSheaf_comp_uliftFunctor_iff]
    apply Functor.op_comp_isSheaf_of_types e.inverse J (Scheme.zariskiTopology.{u})
      (Scheme.zariskiTopology.yoneda.obj X)
  let F : C â¥¤ Sheaf J (Type (u + 1)) := J.yoneda
  have : MonoCoprod C :=
    .monoCoprod_of_preservesCoprod_of_reflectsMono e.inverse
  have {Îº : Type} : HasColimitsOfShape (Discrete Îº) C :=
    Adjunction.hasColimitsOfShape_of_equivalence e.inverse
  have : PreservesColimitsOfShape (Discrete WalkingPair) F := by
    apply J.preservesColimitsOfShape_yoneda_of_ofArrows_inj_mem
    Â· intro X c hc
      simp only [C]
      have : Sieve.ofArrows _ (e.inverse.mapCocone c).Î¹.app âˆˆ
          Scheme.zariskiTopology (e.inverse.mapCocone c).pt :=
        ofArrows_Î¹_mem_zariskiTopology_of_isColimit _ (isColimitOfPreserves e.inverse hc)
      apply GrothendieckTopology.superset_covering _ _ this
      rw [Sieve.ofArrows, Sieve.generate_le_iff]
      rintro - - âŸ¨iâŸ©
      refine âŸ¨X i.1, c.Î¹.app i, ðŸ™ _, ?_, rflâŸ©
      apply Sieve.ofArrows_mk
    Â· intro X c hc i j hij (Y : Scheme.{u}) âŸ¨(a : Y âŸ¶ X i)âŸ© âŸ¨(b : Y âŸ¶ X j)âŸ©
        (hab : e.functor.map a â‰« c.inj i = e.functor.map b â‰« c.inj j)
      constructor
      refine (Limits.IsInitial.isInitialIffObj e.inverse _).invFun (Nonempty.some ?_)
      rw [isInitial_iff_isEmpty]
      refine isEmpty_of_commSq_sigmaÎ¹_of_ne (f := a) (g := b) âŸ¨?_âŸ© hij
      let X' : WalkingPair â†’ Scheme.{u} := X
      let iso : colimit (Discrete.functor X' â‹™ e.functor) â‰… c.pt :=
        IsColimit.coconePointsIsoOfNatIso (colimit.isColimit _) hc Discrete.natIsoFunctor
      have this (i) : e.functor.map (Sigma.Î¹ X' i) = c.inj i â‰« iso.inv â‰«
          (preservesColimitIso e.functor (Discrete.functor X)).inv := by
        simp only [Cofan.inj, IsColimit.coconePointsIsoOfNatIso_inv, IsColimit.Î¹_map_assoc,
          Discrete.functor_obj_eq_as, Functor.comp_obj, Discrete.natIsoFunctor_inv_app,
          colimit.cocone_x, colimit.cocone_Î¹, Î¹_preservesColimitIso_inv, e, X', iso, C]
        rfl
      apply e.functor.map_injective
      rw [Functor.map_comp, this, Functor.map_comp, this, reassoc_of% hab]
    Â· intro Y hY
      simp only [Functor.mem_inducedTopology_sieves_iff, Sieve.functorPushforward_bot, J]
      convert Scheme.bot_mem_grothendieckTopology _
      rw [â† isInitial_iff_isEmpty]
      exact âŸ¨hY.isInitialObj e.inverse YâŸ©
  apply finitaryExtensive_of_preserves_and_reflects (e.functor â‹™ F)

instance {Î¹ : Type*} [Finite Î¹] : Finite (Discrete Î¹) :=
  discreteEquiv.finite_iff.mpr inferInstance

noncomputable instance {Î¹ : Type*} [Small.{u} Î¹] [Finite Î¹] :
    CreatesColimitsOfShape (Discrete Î¹) AffineScheme.forgetToScheme.{u} := by
  let Î¹' := Shrink.{u} Î¹
  suffices CreatesColimitsOfShape (Discrete Î¹') AffineScheme.forgetToScheme.{u} by
    exact createsColimitsOfShapeOfEquiv (Discrete.equivalence (equivShrink Î¹)).symm
      AffineScheme.forgetToScheme
  refine âŸ¨fun {K} â†¦ ?_âŸ©
  have : CreatesColimit (Discrete.functor (K.obj âˆ˜ Discrete.mk))
      AffineScheme.forgetToScheme :=
    createsColimitOfFullyFaithfulOfIso (.mk (âˆ fun i â†¦ (K.obj âŸ¨iâŸ©).1) inferInstance) (Iso.refl _)
  exact createsColimitOfIsoDiagram AffineScheme.forgetToScheme Discrete.natIsoFunctor.symm

instance : FinitaryExtensive AffineScheme.{u} :=
  finitaryExtensive_of_preserves_and_reflects (AffineScheme.forgetToScheme)

end AlgebraicGeometry
