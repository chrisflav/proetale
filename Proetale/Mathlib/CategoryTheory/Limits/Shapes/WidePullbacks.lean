import Mathlib.CategoryTheory.Limits.Shapes.WidePullbacks
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.CommSq
import Proetale.Mathlib.Order.BooleanAlgebra.Set

namespace CategoryTheory.Limits

noncomputable
def WidePullback.reindex {Î± Î² : Type*} {C : Type*} [Category C] {B : C}
    {X : Î± â†’ C} {Y : Î² â†’ C}
    {f : (j : Î±) â†’ X j âŸ¶ B} [HasWidePullback B X f]
    {g : (j : Î²) â†’ Y j âŸ¶ B} [HasWidePullback B Y g]
    (e : Î± â‰ƒ Î²) (s : âˆ€ a, X a â‰… Y (e a))
    (w : âˆ€ i, (s i).hom â‰« g (e i) = f _) :
    widePullback B X f â‰… widePullback B Y g where
  hom := WidePullback.lift (WidePullback.base _)
    (fun i â†¦ WidePullback.Ï€ _ (e.symm i) â‰« (s _).hom â‰« eqToHom (by simp))
    fun i â†¦ by
      obtain âŸ¨i, rflâŸ© := e.surjective i
      simp [w]
  inv := WidePullback.lift (WidePullback.base _)
    (fun i â†¦ WidePullback.Ï€ _ (e i) â‰« (s _).inv)
    fun i â†¦ by simp [â† w]

noncomputable
def WidePullback.proj {Î± Î² : Type*} {C : Type*} [Category C] {B : C}
    {X : Î± âŠ• Î² â†’ C}
    {f : (j : Î± âŠ• Î²) â†’ X j âŸ¶ B} [HasWidePullback B X f]
    [HasWidePullback B (X âˆ˜ Sum.inl) (fun j â†¦ f (.inl j))] :
    widePullback B X f âŸ¶ widePullback B (X âˆ˜ Sum.inl) (fun j â†¦ f (.inl j)) :=
  WidePullback.lift (WidePullback.base _) (fun j â†¦ WidePullback.Ï€ _ _) (by simp)

noncomputable
def WidePullback.mapOfSumEquiv {Î± Î² Î³ : Type*} {C : Type*} [Category C] {B : C}
    {X : Î± â†’ C} {Y : Î² â†’ C}
    {f : (j : Î±) â†’ X j âŸ¶ B} [HasLimitsOfShape (WidePullbackShape Î±) C]
    {g : (j : Î²) â†’ Y j âŸ¶ B} [HasLimitsOfShape (WidePullbackShape Î²) C]
    [HasLimitsOfShape (WidePullbackShape (Î² âŠ• Î³)) C]
    (e : Î² âŠ• Î³ â‰ƒ Î±) (s : âˆ€ (b : Î²), X (e (.inl b)) âŸ¶ Y b)
    (w : âˆ€ b, s b â‰« g b = f _) :
    widePullback B X f âŸ¶ widePullback B Y g :=
  (WidePullback.reindex (Y := X âˆ˜ e) (g := fun i â†¦ f (e i)) e.symm
    (fun a â†¦ eqToIso (by simp)) (fun i â†¦ by
      simp only [Function.comp_apply, eqToIso.hom]
      rw [â† eqToHom_naturality, eqToHom_refl, Category.comp_id]
      rw [Equiv.apply_symm_apply])).hom â‰«
    WidePullback.lift (objs := Sum.elim Y (X âˆ˜ e âˆ˜ .inr))
      (arrows := fun i â†¦ match i with
        | .inl b => g b
        | .inr c => f _)
      (WidePullback.base _)
      (fun j â†¦ match j with
        | .inl b => WidePullback.Ï€ _ _ â‰« s b
        | .inr b => WidePullback.Ï€ _ _)
      (by simp [w]) â‰«
      WidePullback.proj

section

variable {C : Type*} [Category C]

@[simps!]
def WidePullbackShape.functorExt {Î¹ : Type*}
    {F G : WidePullbackShape Î¹ â¥¤ C}
    (base : F.obj none â‰… G.obj none)
    (comp : âˆ€ i, F.obj (some i) â‰… G.obj (some i))
    (w : âˆ€ i, F.map (.term i) â‰« base.hom = (comp i).hom â‰« G.map (.term i) := by cat_disch) :
    F â‰… G :=
  NatIso.ofComponents
    (fun i â†¦ match i with
      | none => base
      | some i => comp i)
    (fun f â†¦ by rcases f <;> simp [w])

@[simp]
lemma WidePullbackShape.equivalenceOfEquiv_functor_obj_none
    {Î¹ Î¹' : Type*} (e : Î¹ â‰ƒ Î¹') :
    (WidePullbackShape.equivalenceOfEquiv _ e).functor.obj none = none := rfl

@[simp]
lemma WidePullbackShape.equivalenceOfEquiv_functor_obj_some
    {Î¹ Î¹' : Type*} (e : Î¹ â‰ƒ Î¹') (i) :
    (WidePullbackShape.equivalenceOfEquiv _ e).functor.obj (some i) = some (e i) := rfl

@[simp]
lemma WidePullbackShape.equivalenceOfEquiv_functor_map_term
    {Î¹ Î¹' : Type*} (e : Î¹ â‰ƒ Î¹') (i) :
    (WidePullbackShape.equivalenceOfEquiv _ e).functor.map (.term i) = .term (e i) := rfl

abbrev WidePullbackCone {Î¹ : Type*} {X : C} {Y : Î¹ â†’ C} (f : âˆ€ i, Y i âŸ¶ X) :=
  Cone (WidePullbackShape.wideCospan X Y f)

namespace WidePullbackCone

variable {Î¹ : Type*} {X : C} {Y : Î¹ â†’ C} {f : âˆ€ i, Y i âŸ¶ X}

abbrev Ï€ (s : WidePullbackCone f) (i : Î¹) : s.pt âŸ¶ Y i :=
  (Cone.Ï€ s).app (some i)

abbrev base (s : WidePullbackCone f) : s.pt âŸ¶ X :=
  (Cone.Ï€ s).app none

@[reassoc (attr := simp)]
lemma condition (s : WidePullbackCone f) (i : Î¹) : s.Ï€ i â‰« f i = s.base := by
  simpa using ((Cone.Ï€ s).naturality (.term i)).symm

@[simps! pt]
def mk {W : C} (b : W âŸ¶ X) (Ï€ : âˆ€ i, W âŸ¶ Y i) (h : âˆ€ i, Ï€ i â‰« f i = b) :
    WidePullbackCone f :=
  WidePullbackShape.mkCone b Ï€ h

@[simp]
lemma mk_base {W : C} (b : W âŸ¶ X) (Ï€ : âˆ€ i, W âŸ¶ Y i) (h : âˆ€ i, Ï€ i â‰« f i = b) :
    (WidePullbackCone.mk b Ï€ h).base = b := rfl

@[simp]
lemma mk_Ï€ {W : C} (b : W âŸ¶ X) (Ï€ : âˆ€ i, W âŸ¶ Y i) (h : âˆ€ i, Ï€ i â‰« f i = b) (i : Î¹) :
    (WidePullbackCone.mk b Ï€ h).Ï€ i = Ï€ i := rfl

def IsLimit.mk (s : WidePullbackCone f) (lift : âˆ€ t : WidePullbackCone f, t.pt âŸ¶ s.pt)
    (facbase : âˆ€ t, lift t â‰« s.base = t.base) (facÏ€ : âˆ€ t i, lift t â‰« s.Ï€ i = t.Ï€ i)
    (uniq : âˆ€ (t) (m : t.pt âŸ¶ s.pt), m â‰« s.base = t.base â†’ (âˆ€ i, m â‰« s.Ï€ i = t.Ï€ i) â†’ m = lift t) :
    IsLimit s where
  lift := lift
  fac t j := by
    cases j
    exact facbase t
    exact facÏ€ t _
  uniq t m hm := uniq _ _ (hm none) fun _ â†¦ hm (some _)

lemma IsLimit.hom_ext {s : WidePullbackCone f} (hs : IsLimit s)
    {W : C} {k l : W âŸ¶ s.pt} (hbase : k â‰« s.base = l â‰« s.base)
    (hÏ€ : âˆ€ i, k â‰« s.Ï€ i = l â‰« s.Ï€ i) :
    k = l := by
  apply hs.hom_ext
  rintro (_ | j)
  Â· exact hbase
  Â· exact hÏ€ j

def IsLimit.lift {s : WidePullbackCone f} (hs : IsLimit s)
    {W : C} (b : W âŸ¶ X) (a : âˆ€ i, W âŸ¶ Y i) (w : âˆ€ i, a i â‰« f i = b) :
    W âŸ¶ s.pt :=
  hs.lift (WidePullbackCone.mk b a w)

@[reassoc (attr := simp)]
lemma IsLimit.lift_base {s : WidePullbackCone f} (hs : IsLimit s)
    {W : C} (b : W âŸ¶ X) (a : âˆ€ i, W âŸ¶ Y i) (w : âˆ€ i, a i â‰« f i = b) :
    IsLimit.lift hs b a w â‰« s.base = b := by
  simp [lift]

@[reassoc (attr := simp)]
lemma IsLimit.lift_Ï€ {s : WidePullbackCone f} (hs : IsLimit s)
    {W : C} (b : W âŸ¶ X) (a : âˆ€ i, W âŸ¶ Y i) (w : âˆ€ i, a i â‰« f i = b) (i : Î¹) :
    IsLimit.lift hs b a w â‰« s.Ï€ i = a i := by
  simp [lift]

@[simps! pt]
def reindex {Î¹ : Type*} {X : C} {Y : Î¹ â†’ C} {f : âˆ€ i, Y i âŸ¶ X} (s : WidePullbackCone f)
    {Î¹' : Type*} (e : Î¹' â‰ƒ Î¹) :
    WidePullbackCone (fun i â†¦ f (e i)) :=
  .mk s.base (fun i â†¦ s.Ï€ _) (by simp)

@[simp]
lemma reindex_base {Î¹ : Type*} {X : C} {Y : Î¹ â†’ C} {f : âˆ€ i, Y i âŸ¶ X} (s : WidePullbackCone f)
    {Î¹' : Type*} (e : Î¹' â‰ƒ Î¹) :
    (s.reindex e).base = s.base := rfl

@[simp]
lemma reindex_Ï€ {Î¹ : Type*} {X : C} {Y : Î¹ â†’ C} {f : âˆ€ i, Y i âŸ¶ X} (s : WidePullbackCone f)
    {Î¹' : Type*} (e : Î¹' â‰ƒ Î¹) (i : Î¹') :
    (s.reindex e).Ï€ i = s.Ï€ (e i) := rfl

def ext {Î¹ : Type*}
    {X : C} {Y : Î¹ â†’ C} {f : âˆ€ i, Y i âŸ¶ X} {s t : WidePullbackCone f}
    (e : s.pt â‰… t.pt)
    (base : e.hom â‰« t.base = s.base)
    (Ï€ : âˆ€ i, e.hom â‰« t.Ï€ i = s.Ï€ i) :
    s â‰… t :=
  Cones.ext e <| by rintro (_ | _) <;> simp [base, Ï€]

def reindexIsLimitEquiv {Î¹ : Type*}
    {X : C} {Y : Î¹ â†’ C} {f : âˆ€ i, Y i âŸ¶ X} (s : WidePullbackCone f) {Î¹' : Type*} (e : Î¹' â‰ƒ Î¹) :
    IsLimit (s.reindex e) â‰ƒ IsLimit s :=
  (IsLimit.whiskerEquivalenceEquiv <| WidePullbackShape.equivalenceOfEquiv _ e.symm).trans <|
    IsLimit.equivOfNatIsoOfIso
      (WidePullbackShape.functorExt (Iso.refl X) (fun i â†¦ eqToIso (by simp))
        fun i â†¦ by simp [â† eqToHom_naturality]) _ _
      (WidePullbackCone.ext (Iso.refl _) (by simp [WidePullbackCone.base, WidePullbackCone.reindex])
        (fun i â†¦ by
          simp [WidePullbackCone.Ï€, WidePullbackCone.reindex,
            eqToHom_naturality (fun i â†¦ (Cone.Ï€ s).app (some i)) (e.apply_symm_apply i)]))

def isPullback_sum {Î± Î² : Type*} {X : C} {Y : Î± âŠ• Î² â†’ C}
    {f : âˆ€ i, Y i âŸ¶ X} (c : WidePullbackCone f)
    (cl : WidePullbackCone (fun i â†¦ f (.inl i))) (cr : WidePullbackCone (fun i â†¦ f (.inr i)))
    (hc : IsLimit c) (hcl : IsLimit cl) (hcr : IsLimit cr) :
    IsPullback
      (WidePullbackCone.IsLimit.lift hcl c.base (fun _ â†¦ c.Ï€ _) (by simp))
      (WidePullbackCone.IsLimit.lift hcr c.base (fun _ â†¦ c.Ï€ _) (by simp))
      cl.base cr.base := by
  refine âŸ¨âŸ¨by simpâŸ©, âŸ¨PullbackCone.IsLimit.mk _ (fun s â†¦ ?_) (fun s â†¦ ?_) (fun s â†¦ ?_) ?_âŸ©âŸ©
  Â· refine WidePullbackCone.IsLimit.lift hc (s.fst â‰« cl.base) ?_ ?_
    Â· intro j
      match j with
      | .inl a => exact s.fst â‰« cl.Ï€ _
      | .inr a => exact s.snd â‰« cr.Ï€ _
    Â· rintro (_ | _) <;> simp [-PullbackCone.condition_one]
  Â· apply WidePullbackCone.IsLimit.hom_ext hcl <;> simp [-PullbackCone.condition_one]
  Â· apply WidePullbackCone.IsLimit.hom_ext hcr <;> simp [-PullbackCone.condition_one, s.condition]
  Â· intro s m h1 h2
    apply WidePullbackCone.IsLimit.hom_ext hc <;> simp [â† h1, â† h2]

def isPullback_of_isCompl {Î± Î² Î³ : Type*} {X : C}
    {Y : Î³ â†’ C} (f : âˆ€ i, Y i âŸ¶ X) (il : Î± â†’ Î³) (ir : Î² â†’ Î³)
    (hlr : IsCompl (Set.range il) (Set.range ir))
    (hil : il.Injective) (hir : ir.Injective)
    (c : WidePullbackCone f)
    (cl : WidePullbackCone fun i â†¦ f (il i)) (cr : WidePullbackCone fun i â†¦ f (ir i))
    (hc : IsLimit c) (hcl : IsLimit cl) (hcr : IsLimit cr) :
    IsPullback
      (WidePullbackCone.IsLimit.lift hcl c.base (fun i â†¦ c.Ï€ (il i)) (by simp))
      (WidePullbackCone.IsLimit.lift hcr c.base (fun i â†¦ c.Ï€ (ir i)) (by simp))
      cl.base cr.base := by
  have : Function.Surjective (Sum.elim il ir) := fun x â†¦ by
    simp_rw [isCompl_iff, Set.codisjoint_iff, â† Set.univ_subset_iff, Set.subset_def, Set.mem_univ,
      forall_const] at hlr
    obtain âŸ¨x, rflâŸ© | âŸ¨x, rflâŸ© := hlr.2 x <;> simp
  let e : Î± âŠ• Î² â‰ƒ Î³ :=
    .ofBijective (Sum.elim il ir) âŸ¨hil.sumElim hir (Set.disjoint_range_iff.mp hlr.1), thisâŸ©
  let c' : WidePullbackCone (fun i â†¦ f (e i)) := c.reindex e
  have hc' : IsLimit c' :=
    (WidePullbackCone.reindexIsLimitEquiv _ _).symm hc
  exact isPullback_sum c' cl cr hc' hcl hcr

lemma isPullback_of_isCompl' {Î± Î² : Type*} {X : C}
    {Y : Î² â†’ C} (f : âˆ€ i, Y i âŸ¶ X) (l : Î± â†’ Î²) (hl : Function.Injective l) (i : Î²)
    (H : IsCompl {i} (Set.range l)) (c : WidePullbackCone f) (hc : IsLimit c)
    (d : WidePullbackCone fun i â†¦ f (l i)) (hd : IsLimit d) :
    IsPullback (c.Ï€ i)
      (WidePullbackCone.IsLimit.lift hd c.base (fun i â†¦ c.Ï€ _) (by simp))
      (f i) d.base := by
  let Î³ := { j : Î² // i â‰  j }
  let cl : WidePullbackCone (fun _ : Unit â†¦ f i) :=
    WidePullbackCone.mk (f i) (fun _ â†¦ ğŸ™ _) (by simp)
  have hcl : IsLimit cl := by
    refine WidePullbackCone.IsLimit.mk _ ?_ ?_ ?_ ?_
    Â· intro t
      exact t.Ï€ ()
    Â· intro t
      simp [cl]
    Â· intro t
      simp [cl]
    Â· intro t m hm h
      simpa [cl] using h ()
  convert c.isPullback_of_isCompl _ _ _ (by simpa)
    (Function.injective_of_subsingleton _) hl cl d hc hcl hd
  apply WidePullbackCone.IsLimit.hom_ext hcl
  Â· simp only [WidePullbackCone.IsLimit.lift_base]
    simp [cl]
  Â· simp only [WidePullbackCone.IsLimit.lift_Ï€]
    simp [cl]

lemma isPullback_ne {Î² : Type*} {X : C}
    {Y : Î² â†’ C} (f : âˆ€ i, Y i âŸ¶ X) (i : Î²)
    [HasWidePullback X Y f] [HasWidePullback X (fun j : { j // i â‰  j } â†¦ Y j) fun j â†¦ f j] :
    IsPullback (WidePullback.Ï€ f _)
      (WidePullback.lift (WidePullback.base _) (fun j â†¦ WidePullback.Ï€ _ _) (by simp))
      (f i) (WidePullback.base <| fun j : {j // i â‰  j } â†¦ f j) := by
  apply isPullback_of_isCompl'
  Â· exact Subtype.val_injective
  Â· rw [isCompl_iff]
    simp [codisjoint_iff]
    grind
  Â· exact limit.isLimit _

end WidePullbackCone

end

end CategoryTheory.Limits
