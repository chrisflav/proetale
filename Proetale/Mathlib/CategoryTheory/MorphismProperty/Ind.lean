/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.CategoryTheory.Limits.Preserves.Over
import Mathlib.CategoryTheory.Limits.Types.Filtered
import Mathlib.CategoryTheory.MorphismProperty.Limits
import Mathlib.CategoryTheory.Presentable.Finite
import Mathlib.CategoryTheory.WithTerminal.Cone

/-!
# Ind and pro-properties

Given a morphism property `P`, we define a morphism property `ind P` that is satisfied for
`f : X ‚ü∂ Y` if `Y` is a filtered colimit of `Y·µ¢` and `f·µ¢ : X ‚ü∂ Y·µ¢` satisfy `P`.

We show that `ind P` inherits stability properties from `P`.

## TODOs:

- Show `ind P` is stable under composition if `P` spreads out (Christian).
-/

universe s t w v u

namespace CategoryTheory

open Limits

variable {C : Type u} [Category.{v} C] (P : MorphismProperty C)

/-- The induced functor to `Under X` from a functor `J ‚•§ C` and natural maps `s·µ¢ : X ‚ü∂ D·µ¢`. -/
@[simps]
def Under.lift {J : Type*} [Category J] (D : J ‚•§ C) {X : C} (s : (Functor.const J).obj X ‚ü∂ D) :
    J ‚•§ Under X where
  obj j := Under.mk (s.app j)
  map f := Under.homMk (D.map f) (by simpa using (s.naturality f).symm)

/-- The induced cocone on `Under X` from on the lifted functor. -/
@[simps]
def Under.liftCocone {J : Type*} [Category J] (D : J ‚•§ C) {X : C} (s : (Functor.const J).obj X ‚ü∂ D)
    (c : Cocone D) (p : X ‚ü∂ c.pt) (hp : ‚àÄ j, s.app j ‚â´ c.Œπ.app j = p) :
    Cocone (Under.lift D s) where
  pt := Under.mk p
  Œπ.app j := Under.homMk (c.Œπ.app j)

/-- The lifted cocone on `Under X` is a colimit cocone if the original cocone was colimiting
and `J` is nonempty. -/
def Under.isColimitLiftCocone {J : Type*} [Category J] [Nonempty J]
    (D : J ‚•§ C) {X : C} (s : (Functor.const J).obj X ‚ü∂ D)
    (c : Cocone D) (p : X ‚ü∂ c.pt) (hp : ‚àÄ j, s.app j ‚â´ c.Œπ.app j = p) :
    IsColimit c ‚Üí IsColimit (Under.liftCocone D s c p hp) := by
  refine fun hc ‚Ü¶ ‚ü®fun s ‚Ü¶ ?_, fun _ _ ‚Ü¶ ?_, ?_‚ü©
  ¬∑ refine CategoryTheory.Under.homMk ?_ ?_
    ¬∑ exact hc.desc ((CategoryTheory.Under.forget _).mapCocone s)
    ¬∑ obtain ‚ü®j‚ü© : Nonempty J := ‚Äπ_‚Ä∫
      simp only [liftCocone_pt, Functor.const_obj_obj, mk_right, Functor.id_obj, mk_hom, ‚Üê hp j,
        Category.assoc, IsColimit.fac, Functor.mapCocone_pt, forget_obj, Functor.mapCocone_Œπ_app,
        lift_obj, forget_map]
      apply CategoryTheory.Under.w (s.Œπ.app j)
  ¬∑ ext; simp [hc.fac]
  ¬∑ intro c m hm
    ext
    refine hc.hom_ext fun j ‚Ü¶ ?_
    simpa [hc.fac] using congr($(hm j).right)

open IsFiltered in
instance {J : Type*} [Category J] [IsFilteredOrEmpty J] : IsFiltered (WithInitial J) where
  cocone_objs x y :=
    match x, y with
    | .star, y => ‚ü®y, ‚ü®‚ü©, ùüô y, trivial‚ü©
    | x, .star => ‚ü®x, ùüô x, ‚ü®‚ü©, trivial‚ü©
    | .of x, .of y => ‚ü®.of <| max x y, leftToMax _ _, rightToMax _ _, trivial‚ü©
  cocone_maps x y f g :=
    match x, y with
    | .star, y => ‚ü®y, ùüô _, rfl‚ü©
    | _, .star => ‚ü®.star, ùüô _, (IsIso.eq_inv_comp f).mp rfl‚ü©
    | .of _, .of _ => ‚ü®.of <| coeq f g, coeqHom _ _, coeq_condition _ _‚ü©

instance (X : C) [HasFilteredColimits C] : ReflectsFilteredColimits (Under.forget X) := by
  constructor
  intro J _ _
  exact reflectsColimitsOfShape_of_reflectsIsomorphisms

open Opposite

lemma IsFinitelyPresentable.exists_hom_of_isColimit {J : Type w} [SmallCategory J] [IsFiltered J]
    {D : J ‚•§ C} {c : Cocone D} (hc : IsColimit c) {X : C} [IsFinitelyPresentable.{w} X]
    (f : X ‚ü∂ c.pt) :
    ‚àÉ (j : J) (p : X ‚ü∂ D.obj j), p ‚â´ c.Œπ.app j = f := by
  have : PreservesFilteredColimitsOfSize.{w, w} (coyoneda.obj (op X)) := by
    rw [‚Üê isFinitelyPresentable_iff_preservesFilteredColimitsOfSize]
    infer_instance
  exact Types.jointly_surjective_of_isColimit (isColimitOfPreserves (coyoneda.obj (op X)) hc) f

lemma IsFinitelyPresentable.exists_eq_of_isColimit {J : Type w} [SmallCategory J] [IsFiltered J]
    {D : J ‚•§ C} {c : Cocone D} (hc : IsColimit c) {X : C} [IsFinitelyPresentable.{w} X]
    {i j : J} (f : X ‚ü∂ D.obj i) (g : X ‚ü∂ D.obj j) (h : f ‚â´ c.Œπ.app i = g ‚â´ c.Œπ.app j) :
    ‚àÉ (k : J) (u : i ‚ü∂ k) (v : j ‚ü∂ k), f ‚â´ D.map u = g ‚â´ D.map v := by
  have : PreservesFilteredColimitsOfSize.{w, w} (coyoneda.obj (op X)) := by
    rw [‚Üê isFinitelyPresentable_iff_preservesFilteredColimitsOfSize]
    infer_instance
  exact
    (Types.FilteredColimit.isColimit_eq_iff _ (isColimitOfPreserves (coyoneda.obj (op X)) hc)).mp h

lemma IsFinitelyPresentable.exists_hom_of_isColimit_under
    {J : Type w} [SmallCategory J] [IsFiltered J] {D : J ‚•§ C} {c : Cocone D} (hc : IsColimit c)
    {X A : C} (p : X ‚ü∂ A) (s : (Functor.const J).obj X ‚ü∂ D)
    [IsFinitelyPresentable.{w} (Under.mk p)]
    (f : A ‚ü∂ c.pt) (h : ‚àÄ (j : J), s.app j ‚â´ c.Œπ.app j = p ‚â´ f) :
    ‚àÉ (j : J) (q : A ‚ü∂ D.obj j), p ‚â´ q = s.app j ‚àß q ‚â´ c.Œπ.app j = f := by
  have : Nonempty J := IsFiltered.nonempty
  let hc' := Under.isColimitLiftCocone D s c (p ‚â´ f) h hc
  obtain ‚ü®j, q, hq‚ü© := exists_hom_of_isColimit (X := Under.mk p) hc' (Under.homMk f rfl)
  use j, q.right, Under.w q, congr($(hq).right)

namespace Limits

/-- A colimit presentation of `X` over `J` is a diagram `{D·µ¢}` in `C` and natural maps
`s·µ¢ : D·µ¢ ‚ü∂ X` making `X` into the colimit of the `D·µ¢`. -/
structure ColimitPresentation (J : Type w) [Category.{t} J] (X : C) where
  /-- The diagram `{D·µ¢}`. -/
  diag : J ‚•§ C
  /-- The natural maps `s·µ¢ : D·µ¢ ‚ü∂ X`. -/
  natTrans : diag ‚ü∂ (Functor.const J).obj X
  /-- `X` is the colimit of the `D·µ¢` via `s·µ¢`. -/
  isColimit : IsColimit (Cocone.mk _ natTrans)

variable {J : Type w} [Category.{t} J] {X : C}

namespace ColimitPresentation

/-- The cocone associated to a colimit presentation. -/
abbrev cocone (pres : ColimitPresentation J X) : Cocone pres.diag :=
  Cocone.mk _ pres.natTrans

/-- The canonical colimit presentation of any object over a point. -/
@[simps]
noncomputable
def self (X : C) : ColimitPresentation PUnit.{s + 1} X where
  diag := (Functor.const _).obj X
  natTrans := ùüô _
  isColimit := isColimitConstCocone _ _

/-- If `F` preserves colimits of shape `J`, it maps colimit presentations of `X` to
colimit presentations of `F(X)`. -/
noncomputable
def map (P : ColimitPresentation J X) {D : Type*} [Category D] (F : C ‚•§ D)
    [PreservesColimitsOfShape J F] : ColimitPresentation J (F.obj X) where
  diag := P.diag ‚ãô F
  natTrans := Functor.whiskerRight P.natTrans F ‚â´ (F.constComp _ _).hom
  isColimit := by
    convert isColimitOfPreserves F P.isColimit
    ext j
    simp

section

variable {J : Type*} {I : J ‚Üí Type*} [Category J] [‚àÄ j, Category (I j)]
  {D : J ‚•§ C} {P : ‚àÄ j, ColimitPresentation (I j) (D.obj j)}

set_option linter.unusedVariables false in
/-- The type underlying the category used in the construction of the composition
of colimit presentations. This is simply `Œ£ j, I j` but with a different category structure. -/
@[nolint unusedArguments]
def Total (P : ‚àÄ j, ColimitPresentation (I j) (D.obj j)) : Type _ :=
  Œ£ j, I j

variable (P) in
/-- Constructor for `Total` to guide type checking. -/
abbrev Total.mk (i : J) (k : I i) : Total P := ‚ü®i, k‚ü©

/-- Morphisms in the `Total` category. -/
@[ext]
structure Total.Hom (k l : Total P) where
  /-- The underlying morphism in the first component. -/
  base : k.1 ‚ü∂ l.1
  /-- A morphism in `C`. -/
  hom : (P k.1).diag.obj k.2 ‚ü∂ (P l.1).diag.obj l.2
  w : (P k.1).natTrans.app k.2 ‚â´ D.map base = hom ‚â´ (P l.1).natTrans.app l.2 := by aesop_cat

attribute [reassoc] Total.Hom.w

/-- Composition of morphisms in the `Total` category. -/
@[simps]
def Total.Hom.comp {k l m : Total P} (f : k.Hom l) (g : l.Hom m) : k.Hom m where
  base := f.base ‚â´ g.base
  hom := f.hom ‚â´ g.hom
  w := by
    simp only [Functor.const_obj_obj, Functor.map_comp, Category.assoc]
    rw [f.w_assoc, g.w]

@[simps! id_base id_hom comp_base comp_hom]
instance : Category (Total P) where
  Hom := Total.Hom
  id _ := { base := ùüô _, hom := ùüô _ }
  comp := Total.Hom.comp

section Small

variable {J : Type w} {I : J ‚Üí Type w} [SmallCategory J] [‚àÄ j, SmallCategory (I j)]
  {D : J ‚•§ C} {P : ‚àÄ j, ColimitPresentation (I j) (D.obj j)}

lemma Total.exists_hom_of_hom {j j' : J} (i : I j) (u : j ‚ü∂ j')
    [IsFiltered (I j')] [IsFinitelyPresentable.{w} ((P j).diag.obj i)] :
    ‚àÉ (i' : I j') (f : Total.mk P j i ‚ü∂ Total.mk P j' i'), f.base = u := by
  obtain ‚ü®i', q, hq‚ü© := IsFinitelyPresentable.exists_hom_of_isColimit (P j').isColimit
    ((P j).natTrans.app i ‚â´ D.map u)
  use i', { base := u, hom := q, w := by simp [‚Üê hq] }

instance [IsFiltered J] [‚àÄ j, IsFiltered (I j)] : Nonempty (Total P) := by
  obtain ‚ü®j‚ü© : Nonempty J := IsFiltered.nonempty
  obtain ‚ü®i‚ü© : Nonempty (I j) := IsFiltered.nonempty
  exact ‚ü®‚ü®j, i‚ü©‚ü©

instance [IsFiltered J] [‚àÄ j, IsFiltered (I j)]
    [‚àÄ j i, IsFinitelyPresentable.{w} ((P j).diag.obj i)] :
    IsFiltered (Total P) where
  cocone_objs k l := by
    let a := IsFiltered.max k.1 l.1
    obtain ‚ü®a', f, hf‚ü© := Total.exists_hom_of_hom (P := P) k.2 (IsFiltered.leftToMax k.1 l.1)
    obtain ‚ü®b', g, hg‚ü© := Total.exists_hom_of_hom (P := P) l.2 (IsFiltered.rightToMax k.1 l.1)
    refine ‚ü®‚ü®a, IsFiltered.max a' b'‚ü©, ?_, ?_, trivial‚ü©
    ¬∑ exact f ‚â´ { base := ùüô _, hom := (P _).diag.map (IsFiltered.leftToMax _ _) }
    ¬∑ exact g ‚â´ { base := ùüô _, hom := (P _).diag.map (IsFiltered.rightToMax _ _) }
  cocone_maps {k l} f g := by
    let a := IsFiltered.coeq f.base g.base
    obtain ‚ü®a', u, hu‚ü© := Total.exists_hom_of_hom (P := P) l.2 (IsFiltered.coeqHom f.base g.base)
    have : (f.hom ‚â´ u.hom) ‚â´ (P _).natTrans.app _ = (g.hom ‚â´ u.hom) ‚â´ (P _).natTrans.app _ := by
      simp only [Category.assoc, Functor.const_obj_obj, ‚Üê u.w, ‚Üê f.w_assoc, ‚Üê g.w_assoc]
      rw [‚Üê Functor.map_comp, hu, IsFiltered.coeq_condition f.base g.base]
      simp
    obtain ‚ü®j, p, q, hpq‚ü© := IsFinitelyPresentable.exists_eq_of_isColimit (P _).isColimit _ _ this
    dsimp at p q
    refine ‚ü®‚ü®a, IsFiltered.coeq p q‚ü©,
      u ‚â´ { base := ùüô _, hom := (P _).diag.map (p ‚â´ IsFiltered.coeqHom p q) }, ?_‚ü©
    apply Total.Hom.ext
    ¬∑ simp [hu, IsFiltered.coeq_condition f.base g.base]
    ¬∑ rw [Category.assoc] at hpq
      simp only [Functor.map_comp, comp_hom, reassoc_of% hpq]
      simp [‚Üê Functor.map_comp, ‚Üê IsFiltered.coeq_condition]

/-- If `P` is a colimit presentation over `J` of `X` and for every `j` we are given a colimit
presentation `Q‚±º` over `I j` of the `P.diag.obj j`, this is the refined colimit presentation of `X`
over `Total Q`. -/
@[simps]
def bind {X : C} (P : ColimitPresentation J X) (Q : ‚àÄ j, ColimitPresentation (I j) (P.diag.obj j))
    [‚àÄ j, IsFiltered (I j)] [‚àÄ j i, IsFinitelyPresentable.{w} ((Q j).diag.obj i)] :
    ColimitPresentation (Total Q) X where
  diag.obj k := (Q k.1).diag.obj k.2
  diag.map {k l} f := f.hom
  natTrans.app k := (Q k.1).natTrans.app k.2 ‚â´ P.natTrans.app k.1
  natTrans.naturality {k l} u := by simp [‚Üê u.w_assoc]
  isColimit.desc c := P.isColimit.desc
    { pt := c.pt
      Œπ.app j := (Q j).isColimit.desc
        { pt := c.pt
          Œπ.app i := c.Œπ.app ‚ü®j, i‚ü©
          Œπ.naturality {i i'} u := by
            let v : Total.mk Q j i ‚ü∂ .mk _ j i' := { base := ùüô _, hom := (Q _).diag.map u }
            simpa using c.Œπ.naturality v }
      Œπ.naturality {j j'} u := by
        refine (Q j).isColimit.hom_ext fun i ‚Ü¶ ?_
        simp only [Functor.const_obj_obj, Functor.const_obj_map, Category.comp_id,
          (Q j).isColimit.fac]
        obtain ‚ü®i', hom, rfl‚ü© := Total.exists_hom_of_hom (P := Q) i u
        rw [reassoc_of% hom.w, (Q j').isColimit.fac]
        simpa using c.Œπ.naturality hom }
  isColimit.fac := fun c ‚ü®j, i‚ü© ‚Ü¶ by simp [P.isColimit.fac, (Q j).isColimit.fac]
  isColimit.uniq c m hm := by
    refine P.isColimit.hom_ext fun j ‚Ü¶ ?_
    simp only [Functor.const_obj_obj, P.isColimit.fac]
    refine (Q j).isColimit.hom_ext fun i ‚Ü¶ ?_
    simpa [(Q j).isColimit.fac] using hm (.mk _ j i)

end Small

end

end ColimitPresentation

/-- A limit presentation of `X` over `J` is a diagram `{D·µ¢}` in `C` and natural maps
`s·µ¢ : X ‚ü∂ D·µ¢` making `X` into the limit of the `D·µ¢`. -/
structure LimitPresentation (J : Type w) [Category.{t} J] (X : C) where
  /-- The diagram `{D·µ¢}`. -/
  diag : J ‚•§ C
  /-- The natural maps `s·µ¢ : X ‚ü∂ D·µ¢`. -/
  natTrans : (Functor.const J).obj X ‚ü∂ diag
  /-- `X` is the limit of the `D·µ¢` via `s·µ¢`. -/
  isColimit : IsLimit (Cone.mk _ natTrans)

end Limits

namespace ObjectProperty

variable {P : ObjectProperty C}

/-- `X` satisfies `ind P` if `X` is a filtered colimit of `X·µ¢` for `X·µ¢` in `P`. -/
def ind (P : ObjectProperty C) : ObjectProperty C :=
  fun X ‚Ü¶ ‚àÉ (J : Type v) (_ : SmallCategory J) (_ : IsFiltered J)
    (pres : ColimitPresentation J X), ‚àÄ i, P (pres.diag.obj i)

variable (P) in
lemma le_ind : P ‚â§ P.ind := by
  intro X hX
  exact ‚ü®PUnit, inferInstance, inferInstance, .self X, by simpa‚ü©

variable (C) in
/-- The compact objects of `C` are the finitely presentable ones. -/
def compact : ObjectProperty C := fun X ‚Ü¶ IsFinitelyPresentable.{v} X

@[simp]
lemma ind_ind (h : P ‚â§ compact C) : P.ind.ind = P.ind := by
  refine le_antisymm (fun X h ‚Ü¶ ?_) (le_ind P.ind)
  choose J Jc Jf pres K Kc Kf pres' hp using h
  have (j : J) (i : K j) : IsFinitelyPresentable ((pres' j).diag.obj i) := h _ (hp _ _)
  exact ‚ü®_, inferInstance, inferInstance, pres.bind pres', fun k ‚Ü¶ by simp [hp]‚ü©

end ObjectProperty

namespace MorphismProperty

/-- The object property on `Under X` induced by a morphism property. -/
abbrev under {X : C} : ObjectProperty (Under X) := fun f ‚Ü¶ P f.hom

/--
Let `P` be a property of morphisms. `P.Ind` is satisfied for `f : X ‚ü∂ Y`
if there exists a family of natural maps `t·µ¢ : X ‚ü∂ Y·µ¢` and `s·µ¢ : Y·µ¢ ‚ü∂ Y` indexed by `J`
such that
- `J` is filtered
- `Y = colim Y·µ¢` via `{s·µ¢}·µ¢`
- `t·µ¢` satisfies `P` for all `i`
- `f = t·µ¢ ‚â´ s·µ¢` for all `i`.

See `CategoryTheory.MorphismProperty.ind_iff_ind_under_mk` for an equivalent characterization
in terms of `Y` as an object of `Under X`.
-/
def ind (P : MorphismProperty C) : MorphismProperty C :=
  fun X Y f ‚Ü¶ ‚àÉ (J : Type v) (_ : SmallCategory J) (_ : IsFiltered J)
    (D : J ‚•§ C) (t : (Functor.const J).obj X ‚ü∂ D) (s : D ‚ü∂ (Functor.const J).obj Y)
    (_ : IsColimit (Cocone.mk _ s)), ‚àÄ j, P (t.app j) ‚àß t.app j ‚â´ s.app j = f

variable (C) in
/-- A morphism `f : X ‚ü∂ Y` is compact if `Y` is compact viewed as an object of `Under X`. -/
def compact : MorphismProperty C :=
  fun _ _ f ‚Ü¶ IsFinitelyPresentable.{v} (CategoryTheory.Under.mk f)

lemma exists_hom_of_compact {J : Type v} [SmallCategory J] [IsFiltered J] {D : J ‚•§ C} {c : Cocone D}
    (hc : IsColimit c) {X A : C} {p : X ‚ü∂ A} (hp : compact C p) (s : (Functor.const J).obj X ‚ü∂ D)
    (f : A ‚ü∂ c.pt) (h : ‚àÄ (j : J), s.app j ‚â´ c.Œπ.app j = p ‚â´ f) :
    ‚àÉ (j : J) (q : A ‚ü∂ D.obj j), p ‚â´ q = s.app j ‚àß q ‚â´ c.Œπ.app j = f :=
  hp.exists_hom_of_isColimit_under hc _ s _ h

lemma le_ind : P ‚â§ P.ind := by
  intro X Y f hf
  refine ‚ü®PUnit, inferInstance, inferInstance, (Functor.const PUnit).obj Y, ?_, ùüô _, ?_, ?_‚ü©
  ¬∑ exact { app _ := f }
  ¬∑ exact isColimitConstCocone _ _
  ¬∑ simpa

variable {P}

lemma ind_iff_ind_under_mk {X Y : C} (f : X ‚ü∂ Y) :
    P.ind f ‚Üî P.under.ind (CategoryTheory.Under.mk f) := by
  refine ‚ü®fun ‚ü®J, _, _, D, t, s, hs, hst‚ü© ‚Ü¶ ?_, fun ‚ü®J, _, _, pres, hpres‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ refine ‚ü®J, ‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫, ‚ü®?_, ?_, ?_‚ü©, ?_‚ü©
    ¬∑ exact Under.lift D t
    ¬∑ exact { app j := CategoryTheory.Under.homMk (s.app j) (by simp [hst]) }
    ¬∑ have : Nonempty J := IsFiltered.nonempty
      refine Under.isColimitLiftCocone _ _ _ _ (by simp [hst]) hs
    ¬∑ simp [under, hst]
  ¬∑ refine ‚ü®J, ‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫, pres.diag ‚ãô CategoryTheory.Under.forget _, ?_, ?_, ?_, fun j ‚Ü¶ ‚ü®?_, ?_‚ü©‚ü©
    ¬∑ exact { app j := (pres.diag.obj j).hom }
    ¬∑ exact Functor.whiskerRight pres.natTrans (CategoryTheory.Under.forget X)
    ¬∑ exact isColimitOfPreserves (CategoryTheory.Under.forget _) pres.isColimit
    ¬∑ exact hpres j
    ¬∑ simp

lemma ind_under_eq_under_ind (X : C) : P.ind.under (X := X) = P.under.ind := by
  ext f
  simp [under, show f = CategoryTheory.Under.mk f.hom from rfl, ind_iff_ind_under_mk]

variable (Q : MorphismProperty C)

instance [P.RespectsLeft Q] : P.ind.RespectsLeft Q where
  precomp {X Y Z} i hi f := fun ‚ü®J, _, _, D, t, s, hs, hst‚ü© ‚Ü¶ by
    refine ‚ü®J, ‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫, D, (Functor.const J).map i ‚â´ t, s, hs, fun j ‚Ü¶ ‚ü®?_, by simp [hst]‚ü©‚ü©
    exact RespectsLeft.precomp _ hi _ (hst j).1

instance [P.RespectsIso] : P.ind.RespectsIso where
  postcomp {X Y Z} i (hi : IsIso i) f := fun ‚ü®J, _, _, D, t, s, hs, hst‚ü© ‚Ü¶ by
    refine ‚ü®J, ‚Äπ_‚Ä∫, ‚Äπ_‚Ä∫, D, t, s ‚â´ (Functor.const J).map i, ?_, fun j ‚Ü¶ ‚ü®(hst j).1, ?_‚ü©‚ü©
    ¬∑ exact (IsColimit.equivIsoColimit (Cocones.ext (asIso i))) hs
    ¬∑ simp [reassoc_of% (hst j).2]

lemma ind_under_pushout {X Y : C} (g : X ‚ü∂ Y) [HasPushouts C] [P.IsStableUnderCobaseChange]
    {f : Under X} (hf : P.under.ind f) : P.under.ind ((Under.pushout g).obj f) := by
  obtain ‚ü®J, _, _, pres, hpres‚ü© := hf
  use J, inferInstance, inferInstance, pres.map (Under.pushout g)
  intro i
  exact P.pushout_inr _ _ (hpres i)

instance [P.IsStableUnderCobaseChange] [HasPushouts C] : P.ind.IsStableUnderCobaseChange := by
  refine .mk' fun A B A' f g _ hf ‚Ü¶ ?_
  rw [ind_iff_ind_under_mk] at hf ‚ä¢
  exact ind_under_pushout g hf

/-- `ind` is idempotent if `P` implies compact. -/
lemma ind_ind (hp : P ‚â§ compact C) : P.ind.ind = P.ind := by
  refine le_antisymm (fun X Y f hf ‚Ü¶ ?_) P.ind.le_ind
  have : P.under ‚â§ ObjectProperty.compact (Under X) := fun f hf ‚Ü¶ hp _ hf
  simpa [ind_iff_ind_under_mk, ind_under_eq_under_ind, ObjectProperty.ind_ind this] using hf

/--
Let `P` be a property of morphisms. `P.Pro` is satisfied for `f : X ‚ü∂ Y`
if there exists a family of natural maps `t·µ¢ : X·µ¢ ‚ü∂ Y` and `s·µ¢ : X ‚ü∂ X·µ¢` indexed by `J`
such that
- `J` is cofiltered
- `X = lim X·µ¢` via `{s·µ¢}·µ¢`
- `t·µ¢` satisfies `P` for all `i`
- `f = s·µ¢ ‚â´ t·µ¢` for all `i`.
-/
def pro (P : MorphismProperty C) : MorphismProperty C :=
  fun X Y f ‚Ü¶ ‚àÉ (J : Type v) (_ : SmallCategory J) (_ : IsCofiltered J)
    (D : J ‚•§ C) (t : D ‚ü∂ (Functor.const J).obj Y) (s : (Functor.const J).obj X ‚ü∂ D)
    (_ : IsLimit (Cone.mk _ s)), ‚àÄ j, P (t.app j) ‚àß s.app j ‚â´ t.app j = f

lemma pro_eq_unop_ind_op : pro P = (ind P.op).unop := by
  ext X Y f
  refine ‚ü®fun ‚ü®J, _, _, D, t, s, hs, hst‚ü© ‚Ü¶ ?_, fun ‚ü®J, _, _, D, t, s, hs, hst‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ exact ‚ü®J·µí·µñ, inferInstance, inferInstance, D.op, NatTrans.op t,
      NatTrans.op s, isColimitOfUnop hs, fun j ‚Ü¶ ‚ü®(hst j.1).1, by simp [‚Üê (hst j.1).2]‚ü©‚ü©
  ¬∑ exact ‚ü®J·µí·µñ, inferInstance, inferInstance, D.leftOp, NatTrans.leftOp t,
      NatTrans.leftOp s, isLimitOfCoconeRightOpOfCone D.leftOp hs, fun j ‚Ü¶ ‚ü®(hst _).1,
      op_injective (hst _).2‚ü©‚ü©

end CategoryTheory.MorphismProperty
