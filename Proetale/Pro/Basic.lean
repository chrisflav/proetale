/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib

/-!
# Sheafs in the pro category

In this file we develop conditions for when a presheaf on the pro-category
is a sheaf.
-/

universe s t w v‚ÇÅ v‚ÇÇ u‚ÇÅ u‚ÇÇ

namespace CategoryTheory
open Limits

noncomputable
def Limits.IsLimit.ofPreservesLimitColim {J K C : Type*} [Category J] [Category K] [Category C]
    [HasColimitsOfShape J C]
    {D : J ‚•§ K ‚•§ C} [PreservesLimit D.flip colim]
    (c : Cone D.flip) (hc : IsLimit c)
    (d : Cocone D) (hd : IsColimit d)
    (f : Cocone c.pt) (hf : IsColimit f)
    (e : Cone d.pt)
    (iso : f.pt ‚âÖ e.pt)
    (w : ‚àÄ j k, (c.œÄ.app k).app j ‚â´ (d.Œπ.app j).app k = f.Œπ.app j ‚â´ iso.hom ‚â´ e.œÄ.app k) :
    IsLimit e :=
  let natiso : D.flip ‚ãô colim ‚âÖ d.pt :=
    (Limits.colimitIsoFlipCompColim _).symm ‚â™‚â´ (colimit.isColimit _).coconePointUniqueUpToIso hd
  let hc' : IsLimit (colim.mapCone c) := isLimitOfPreserves _ hc
  let t : (Cones.postcompose natiso.hom).obj (colim.mapCone c) ‚âÖ e := by
    refine Cones.ext (IsColimit.coconePointUniqueUpToIso (colimit.isColimit _) hf ‚â™‚â´ iso) ?_
    intro k
    apply colimit.hom_ext
    intro j
    simp only [Iso.trans_hom, Iso.symm_hom, Cones.postcompose_obj_pt, Functor.mapCone_pt,
      colim_obj, Cones.postcompose_obj_œÄ, NatTrans.comp_app, Functor.const_obj_obj,
      Functor.comp_obj, Functor.mapCone_œÄ_app, colim_map, colimitIsoFlipCompColim_inv_app,
      Œπ_colimMap_assoc, Functor.flip_obj_obj, Category.assoc,
      colimit.comp_coconePointUniqueUpToIso_hom_assoc, natiso]
    erw [colimitObjIsoColimitCompEvaluation_Œπ_inv_assoc]
    simp [‚Üê NatTrans.comp_app, w]
  IsLimit.equivOfNatIsoOfIso natiso _ e t hc'

end CategoryTheory

namespace CategoryTheory

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C] {D : Type u‚ÇÇ} [Category.{v‚ÇÇ} D] (F : C ‚•§ D)

namespace Limits

structure RelativeColimitPresentation (J : Type w) [Category.{t} J] (F : C ‚•§ D) (X : D) where
  diag : J ‚•§ C
  Œπ : diag ‚ãô F ‚ü∂ (Functor.const J).obj X
  isColimit : IsColimit (Cocone.mk _ Œπ)

variable {J : Type w} [Category.{t} J] {F : C ‚•§ D} {X : D}

namespace RelativeColimitPresentation

initialize_simps_projections RelativeColimitPresentation (-isColimit)

abbrev cocone (pres : RelativeColimitPresentation J F X) : Cocone (pres.diag ‚ãô F) :=
  Cocone.mk _ pres.Œπ

/-- Forget the fact that the diagram factors through `F`. -/
def colimitPresentation (pres : RelativeColimitPresentation J F X) : ColimitPresentation J X where
  diag := pres.diag ‚ãô F
  Œπ := pres.Œπ
  isColimit := pres.isColimit

noncomputable def self (X : C) : RelativeColimitPresentation PUnit.{s + 1} F (F.obj X) where
  diag := (Functor.const _).obj X
  Œπ := (F.constComp _ X).hom
  isColimit := .equivOfNatIsoOfIso (F.constComp _ X).symm _
    (Cocone.mk _ (F.constComp _ X).hom) (Cocones.ext (Iso.refl _))
    (isColimitConstCocone PUnit.{s + 1} (F.obj X))

/-- A morphism between relative colimit presentations is a natural transformation between
the diagrams. -/
@[ext]
structure Hom {X Y : D} (pres‚ÇÅ : RelativeColimitPresentation J F X)
    (pres‚ÇÇ : RelativeColimitPresentation J F Y) where
  natTrans : pres‚ÇÅ.diag ‚ü∂ pres‚ÇÇ.diag

namespace Hom

variable {X Y Z : D} {pres‚ÇÅ : RelativeColimitPresentation J F X}
    {pres‚ÇÇ : RelativeColimitPresentation J F Y} {pres‚ÇÉ : RelativeColimitPresentation J F Z}

@[simps]
def id (pres : RelativeColimitPresentation J F X) : pres.Hom pres where
  natTrans := ùüô _

@[simps]
def comp (f : pres‚ÇÅ.Hom pres‚ÇÇ) (g : pres‚ÇÇ.Hom pres‚ÇÉ) : pres‚ÇÅ.Hom pres‚ÇÉ where
  natTrans := f.natTrans ‚â´ g.natTrans

def map (f : pres‚ÇÅ.Hom pres‚ÇÇ) : X ‚ü∂ Y :=
  pres‚ÇÅ.isColimit.map pres‚ÇÇ.cocone (Functor.whiskerRight f.natTrans F)

variable (f : pres‚ÇÅ.Hom pres‚ÇÇ)

@[reassoc (attr := simp)]
lemma Œπ_map (j : J) : pres‚ÇÅ.Œπ.app j ‚â´ f.map = F.map (f.natTrans.app j) ‚â´ pres‚ÇÇ.Œπ.app j :=
  pres‚ÇÅ.isColimit.Œπ_map _ _ j

end Hom

end RelativeColimitPresentation

structure RelativeLimitPresentation (J : Type w) [Category.{t} J] (F : C ‚•§ D) (X : D) where
  diag : J ‚•§ C
  œÄ : (Functor.const J).obj X ‚ü∂ diag ‚ãô F
  isLimit : IsLimit (Cone.mk _ œÄ)

variable {J : Type w} [Category.{t} J] {F : C ‚•§ D} {X : D}

namespace RelativeLimitPresentation

initialize_simps_projections RelativeLimitPresentation (-isLimit)

abbrev cone (pres : RelativeLimitPresentation J F X) : Cone (pres.diag ‚ãô F) :=
  Cone.mk _ pres.œÄ

/-- Forget the fact that the diagram factors through `F`. -/
def colimitPresentation (pres : RelativeLimitPresentation J F X) : LimitPresentation J X where
  diag := pres.diag ‚ãô F
  œÄ := pres.œÄ
  isLimit := pres.isLimit

noncomputable def self (X : C) : RelativeLimitPresentation PUnit.{s + 1} F (F.obj X) where
  diag := (Functor.const _).obj X
  œÄ := (F.constComp _ X).inv
  isLimit := .equivOfNatIsoOfIso (F.constComp _ X).symm _
    (Cone.mk _ (F.constComp _ X).inv) (Cones.ext (Iso.refl _))
    (isLimitConstCone PUnit.{s + 1} (F.obj X))

/-- A morphism between relative colimit presentations is a natural transformation between
the diagrams. -/
@[ext]
structure Hom {X Y : D} (pres‚ÇÅ : RelativeLimitPresentation J F X)
    (pres‚ÇÇ : RelativeLimitPresentation J F Y) where
  natTrans : pres‚ÇÅ.diag ‚ü∂ pres‚ÇÇ.diag

namespace Hom

variable {X Y Z : D} {pres‚ÇÅ : RelativeLimitPresentation J F X}
    {pres‚ÇÇ : RelativeLimitPresentation J F Y} {pres‚ÇÉ : RelativeLimitPresentation J F Z}

@[simps]
def id (pres : RelativeLimitPresentation J F X) : pres.Hom pres where
  natTrans := ùüô _

@[simps]
def comp (f : pres‚ÇÅ.Hom pres‚ÇÇ) (g : pres‚ÇÇ.Hom pres‚ÇÉ) : pres‚ÇÅ.Hom pres‚ÇÉ where
  natTrans := f.natTrans ‚â´ g.natTrans

def map (f : pres‚ÇÅ.Hom pres‚ÇÇ) : X ‚ü∂ Y :=
  pres‚ÇÇ.isLimit.map pres‚ÇÅ.cone (Functor.whiskerRight f.natTrans F)

variable (f : pres‚ÇÅ.Hom pres‚ÇÇ)

@[reassoc (attr := simp)]
lemma map_œÄ (j : J) : f.map ‚â´ pres‚ÇÇ.œÄ.app j = pres‚ÇÅ.œÄ.app j ‚â´ F.map (f.natTrans.app j) :=
  pres‚ÇÇ.isLimit.map_œÄ _ _ j

end Hom

end RelativeLimitPresentation

end Limits

open Limits Opposite

class Functor.IsInd (F : C ‚•§ D) : Prop where
  hom_exists {X Y : D} (f : X ‚ü∂ Y) : ‚àÉ (J : Type w) (_ : SmallCategory J) (_ : IsFiltered J)
    (pres‚ÇÅ : RelativeColimitPresentation J F X) (pres‚ÇÇ : RelativeColimitPresentation J F Y)
    (t : pres‚ÇÅ.Hom pres‚ÇÇ), f = t.map

lemma Functor.IsInd.obj_exists [IsInd.{w} F] (X : D) :
    ‚àÉ (J : Type w) (_ : SmallCategory J) (_ : IsFiltered J),
      Nonempty (RelativeColimitPresentation J F X) := by
  obtain ‚ü®J, _, _, pres, _, _, _‚ü© := hom_exists (F := F) (ùüô X)
  use J, inferInstance, inferInstance
  exact ‚ü®pres‚ü©

instance : Functor.IsInd.{v‚ÇÅ} (Ind.yoneda (C := C)) where
  hom_exists {X Y} f := by
    obtain ‚ü®J, _, _, D‚ÇÅ, D‚ÇÇ, œÜ, ‚ü®e‚ü©‚ü© := Ind.exists_nonempty_arrow_mk_iso_ind_lim (f := f)
    use J, inferInstance, inferInstance
    let pres‚ÇÅ : RelativeColimitPresentation J Ind.yoneda X :=
    { diag := D‚ÇÅ
      Œπ := (colimit.cocone (D‚ÇÅ ‚ãô Ind.yoneda)).Œπ ‚â´
        (Functor.const J).map (Arrow.leftFunc.mapIso e).inv
      isColimit := (colimit.isColimit _).extendIso _ }
    let pres‚ÇÇ : RelativeColimitPresentation J Ind.yoneda Y :=
    { diag := D‚ÇÇ
      Œπ := (colimit.cocone (D‚ÇÇ ‚ãô Ind.yoneda)).Œπ ‚â´
        (Functor.const J).map (Arrow.rightFunc.mapIso e).inv
      isColimit := (colimit.isColimit _).extendIso _ }
    refine ‚ü®pres‚ÇÅ, pres‚ÇÇ, ‚ü®œÜ‚ü©, ?_‚ü©
    apply pres‚ÇÅ.isColimit.hom_ext
    intro j
    simp only [Functor.comp_obj, Functor.const_obj_obj, RelativeColimitPresentation.Hom.Œπ_map]
    simp [pres‚ÇÅ, pres‚ÇÇ, Ind.lim]

class Functor.IsPro (F : C ‚•§ D) : Prop where
  hom_exists {X Y : D} (f : X ‚ü∂ Y) : ‚àÉ (J : Type w) (_ : SmallCategory J) (_ : IsCofiltered J)
    (pres‚ÇÅ : RelativeLimitPresentation J F X) (pres‚ÇÇ : RelativeLimitPresentation J F Y)
    (t : pres‚ÇÅ.Hom pres‚ÇÇ), f = t.map

def PreOneHypercover.functor (F : C ‚•§ D) (X : C) : PreOneHypercover.{w} X ‚•§ PreOneHypercover.{w} (F.obj X) where
  obj E := E.map F
  map {E G} f :=
    { s‚ÇÄ := f.s‚ÇÄ
      h‚ÇÄ i := F.map (f.h‚ÇÄ i)
      s‚ÇÅ k := f.s‚ÇÅ k
      h‚ÇÅ k := F.map (f.h‚ÇÅ k)
      w‚ÇÄ i := by simp [‚Üê Functor.map_comp]
      w‚ÇÅ‚ÇÅ := by simp [‚Üê Functor.map_comp, Hom.w‚ÇÅ‚ÇÅ]
      w‚ÇÅ‚ÇÇ := by simp [‚Üê Functor.map_comp, Hom.w‚ÇÅ‚ÇÇ] }
  -- TODO: improve these proofs by adding some `simp` lemmas
  map_id E := by apply Hom.ext <;> simp <;> rfl
  map_comp {E‚ÇÅ E‚ÇÇ E‚ÇÉ} f g := by apply Hom.ext <;> simp <;> rfl

structure PreOneHypercover.RelativeLimitPresentation (J : Type*) [Category J]
    {X : D} (F : C ‚•§ D) (E : PreOneHypercover.{w} X) where
  pres : Limits.RelativeLimitPresentation J F X
  pres‚ÇÄ : ‚àÄ i : E.I‚ÇÄ, Limits.RelativeLimitPresentation J F (E.X i)
  pres‚ÇÅ : ‚àÄ {i j} (k : E.I‚ÇÅ i j), Limits.RelativeLimitPresentation J F (E.Y k)
  f : ‚àÄ i, (pres‚ÇÄ i).Hom pres
  p‚ÇÅ : ‚àÄ {i j} (k : E.I‚ÇÅ i j), (pres‚ÇÅ k).Hom (pres‚ÇÄ i)
  p‚ÇÇ : ‚àÄ {i j} (k : E.I‚ÇÅ i j), (pres‚ÇÅ k).Hom (pres‚ÇÄ j)
  hf : ‚àÄ i, (f i).map = E.f i
  hp‚ÇÅ : ‚àÄ {i j} (k : E.I‚ÇÅ i j), (p‚ÇÅ k).map = E.p‚ÇÅ k
  hp‚ÇÇ : ‚àÄ {i j} (k : E.I‚ÇÅ i j), (p‚ÇÇ k).map = E.p‚ÇÇ k
  w : ‚àÄ {i j} (k : E.I‚ÇÅ i j), (p‚ÇÅ k).comp (f i) = (p‚ÇÇ k).comp (f j)

@[simps]
def PreOneHypercover.RelativeLimitPresentation.preOneHypercover {J : Type*} [Category J]
    {X : D} {F : C ‚•§ D} {E : PreOneHypercover.{w} X}
    (pres : E.RelativeLimitPresentation J F) (j : J) :
    PreOneHypercover (pres.pres.diag.obj j) where
  I‚ÇÄ := E.I‚ÇÄ
  X i := (pres.pres‚ÇÄ i).diag.obj j
  f i := (pres.f i).natTrans.app j
  I‚ÇÅ := E.I‚ÇÅ
  Y _ _ k := (pres.pres‚ÇÅ k).diag.obj j
  p‚ÇÅ _ _ k := (pres.p‚ÇÅ k).natTrans.app j
  p‚ÇÇ _ _ k := (pres.p‚ÇÇ k).natTrans.app j
  w _ _ k := by simpa using congr($(pres.w k).natTrans.app j)

variable {J : Type*} [Category J] {X : D} {F : C ‚•§ D} {E : PreOneHypercover.{w} X}
  {K : GrothendieckTopology C}

def PreOneHypercover.RelativeLimitPresentation.oneHypercover
    (pres : E.RelativeLimitPresentation J F) (j : J)
    (mem‚ÇÄ : (pres.preOneHypercover j).sieve‚ÇÄ ‚àà K _)
    (mem‚ÇÅ : ‚àÄ {a b : E.I‚ÇÄ} {W : C} (p‚ÇÅ : W ‚ü∂ (pres.preOneHypercover j).X a)
        (p‚ÇÇ : W ‚ü∂ (pres.preOneHypercover j).X b),
        p‚ÇÅ ‚â´ (pres.preOneHypercover j).f a = p‚ÇÇ ‚â´ (pres.preOneHypercover j).f b ‚Üí
        (pres.preOneHypercover j).sieve‚ÇÅ p‚ÇÅ p‚ÇÇ ‚àà K W) :
    K.OneHypercover (pres.pres.diag.obj j) where
  __ := pres.preOneHypercover j
  mem‚ÇÄ := mem‚ÇÄ
  mem‚ÇÅ _ _ _ := mem‚ÇÅ

variable {A : Type*} [Category A]

@[simps]
def PreOneHypercover.RelativeLimitPresentation.multicospan (pres : E.RelativeLimitPresentation J F)
    (P : D·µí·µñ ‚•§ A) :
    J·µí·µñ ‚•§ (WalkingMulticospan E.multicospanShape ‚•§ A) where
  obj j := ((pres.preOneHypercover j.unop).multicospanIndex (F.op ‚ãô P)).multicospan
  map {i j} f :=
    { app
        | .left k => P.map (F.map <| (pres.pres‚ÇÄ k).diag.map f.unop).op
        | .right k => P.map (F.map <| (pres.pres‚ÇÅ k.2).diag.map f.unop).op
      naturality k l f := by
        rcases f
        ¬∑ simp
        ¬∑ simp [‚Üê Functor.map_comp, ‚Üê op_comp]
        ¬∑ simp [‚Üê Functor.map_comp, ‚Üê op_comp] }

@[simps]
def PreOneHypercover.RelativeLimitPresentation.cocone (pres : E.RelativeLimitPresentation J F)
    (P : D·µí·µñ ‚•§ A) :
    Cocone (pres.multicospan P) where
  pt := (E.multicospanIndex P).multicospan
  Œπ.app j :=
    { app
        | .left k => P.map ((pres.pres‚ÇÄ k).œÄ.app j.unop).op
        | .right k => P.map ((pres.pres‚ÇÅ k.2).œÄ.app j.unop).op
      naturality k l f := by
        match f with
        | .id _ => simp
        | .fst k =>
          suffices h : P.map (F.map ((pres.p‚ÇÅ k.snd).natTrans.app (unop j))).op ‚â´
              P.map ((pres.pres‚ÇÅ k.snd).œÄ.app (unop j)).op =
                P.map ((pres.pres‚ÇÄ k.fst.1).œÄ.app (unop j)).op ‚â´ P.map (E.p‚ÇÅ k.snd).op by
            simpa
          simp_rw [‚Üê Functor.map_comp, ‚Üê op_comp]
          rw [‚Üê pres.hp‚ÇÅ k.2]
          simp
        | .snd k =>
          suffices h : P.map (F.map ((pres.p‚ÇÇ k.snd).natTrans.app (unop j))).op ‚â´
              P.map ((pres.pres‚ÇÅ k.snd).œÄ.app (unop j)).op =
                P.map ((pres.pres‚ÇÄ k.fst.2).œÄ.app (unop j)).op ‚â´ P.map (E.p‚ÇÇ k.snd).op by
            simpa
          simp_rw [‚Üê Functor.map_comp, ‚Üê op_comp]
          rw [‚Üê pres.hp‚ÇÇ k.2]
          simp
    }
  Œπ.naturality i j f := by
    ext k
    match k with
    | .left k =>
        simp only [multicospan_obj, MulticospanIndex.multicospan_obj, multicospanShape_L,
          preOneHypercover_I‚ÇÄ, multicospanIndex_left, preOneHypercover_X, Functor.comp_obj,
          Functor.op_obj, multicospanShape_R, multicospanIndex_right, preOneHypercover_I‚ÇÅ,
          preOneHypercover_Y, Functor.const_obj_obj, NatTrans.comp_app, multicospan_map_app,
          ‚Üê Functor.map_comp, ‚Üê op_comp, Functor.const_obj_map, Category.comp_id]
        congr 2
        simpa using ((pres.pres‚ÇÄ k).œÄ.naturality f.unop).symm
    | .right k =>
        simp only [multicospan_obj, MulticospanIndex.multicospan_obj, multicospanShape_L,
          preOneHypercover_I‚ÇÄ, multicospanIndex_left, preOneHypercover_X, Functor.comp_obj,
          Functor.op_obj, multicospanShape_R, multicospanIndex_right, preOneHypercover_I‚ÇÅ,
          preOneHypercover_Y, Functor.const_obj_obj, NatTrans.comp_app, multicospan_map_app,
          ‚Üê Functor.map_comp, ‚Üê op_comp, Functor.const_obj_map, Category.comp_id]
        congr 2
        simpa using ((pres.pres‚ÇÅ k.2).œÄ.naturality f.unop).symm

noncomputable
def PreOneHypercover.RelativeLimitPresentation.isColimit (pres : E.RelativeLimitPresentation J F)
    (P : D·µí·µñ ‚•§ A) [PreservesColimitsOfShape J·µí·µñ P] :
    IsColimit (pres.cocone P) :=
  evaluationJointlyReflectsColimits _ fun k ‚Ü¶ match k with
    | .left k => isColimitOfPreserves P (pres.pres‚ÇÄ k).isLimit.op
    | .right k => isColimitOfPreserves P (pres.pres‚ÇÅ k.2).isLimit.op

@[simps]
def PreOneHypercover.RelativeLimitPresentation.multifork (pres : E.RelativeLimitPresentation J F)
    (P : D·µí·µñ ‚•§ A) :
    Cone (pres.multicospan P).flip where
  pt := pres.pres.diag.op ‚ãô F.op ‚ãô P
  œÄ.app
    | .left k =>
      Functor.whiskerRight (NatTrans.op (pres.f k).natTrans) _
    | .right k =>
      Functor.whiskerRight (NatTrans.op <| (pres.p‚ÇÅ k.2).natTrans ‚â´ (pres.f k.1.1).natTrans) _
  œÄ.naturality k l f := by
    rcases f
    ¬∑ simp
    ¬∑ ext; simp
    ¬∑ ext
      simp only [multicospanShape_snd, Functor.const_obj_obj, Functor.comp_obj, Functor.op_obj,
        Functor.flip_obj_obj, multicospan_obj, MulticospanIndex.multicospan_obj, multicospanShape_L,
        preOneHypercover_I‚ÇÄ, multicospanIndex_left, preOneHypercover_X, multicospanShape_R,
        multicospanIndex_right, preOneHypercover_I‚ÇÅ, preOneHypercover_Y, Functor.const_obj_map,
        NatTrans.op_comp, Functor.whiskerRight_comp, Category.id_comp, NatTrans.comp_app,
        Functor.whiskerRight_app, NatTrans.op_app, Functor.comp_map, Functor.op_map,
        Quiver.Hom.unop_op, Functor.flip_map_app, MulticospanIndex.multicospan_map,
        multicospanIndex_snd, preOneHypercover_p‚ÇÇ, ‚Üê Functor.map_comp, ‚Üê op_comp]
      congr 3
      simpa using congr($(pres.w _).natTrans.app _)

noncomputable
def PreOneHypercover.RelativeLimitPresentation.isLimit (P : D·µí·µñ ‚•§ A)
    [PreservesColimitsOfShape J·µí·µñ P]
    [HasColimitsOfShape J·µí·µñ A]
    [PreservesLimitsOfShape (WalkingMulticospan E.multicospanShape) (colim : (J·µí·µñ ‚•§ A) ‚•§ _)]
    (pres : E.RelativeLimitPresentation J F)
    (h : ‚àÄ j, IsLimit <| (pres.preOneHypercover j).multifork (F.op ‚ãô P)) :
    IsLimit (E.multifork P) := by
  refine IsLimit.ofPreservesLimitColim (pres.multifork P) ?_ (pres.cocone P) (pres.isColimit P)
      (P.mapCocone pres.pres.cone.op) (isColimitOfPreserves _ pres.pres.isLimit.op) _ (.refl _) ?_
  ¬∑ refine evaluationJointlyReflectsLimits _ fun j ‚Ü¶ ?_
    refine IsLimit.ofIsoLimit (h j.1) (Cones.ext (Iso.refl _) ?_)
    intro l
    simp only [Functor.const_obj_obj, MulticospanIndex.multicospan_obj, multicospanShape_L,
      preOneHypercover_I‚ÇÄ, multicospanIndex_left, preOneHypercover_X, Functor.comp_obj,
      Functor.op_obj, multicospanShape_R, multicospanIndex_right, preOneHypercover_I‚ÇÅ,
      preOneHypercover_Y, Functor.mapCone_pt, multifork_pt, evaluation_obj_obj, Iso.refl_hom,
      Functor.mapCone_œÄ_app, multifork_œÄ_app, NatTrans.op_comp, Functor.whiskerRight_comp,
      evaluation_obj_map, Category.id_comp]
    cases l <;> rfl
  ¬∑ intro j k
    match k with
    | .left k =>
      suffices h : (pres.pres‚ÇÄ k).œÄ.app (unop j) ‚â´ F.map ((pres.f k).natTrans.app (unop j)) =
          E.f k ‚â´ pres.pres.œÄ.app (unop j) by
        simp [h, ‚Üê Functor.map_comp, ‚Üê op_comp, PreOneHypercover.multifork]
      rw [‚Üê pres.hf]
      simp
    | .right k =>
      suffices h : (pres.pres‚ÇÅ k.snd).œÄ.app (unop j) ‚â´
          F.map ((pres.p‚ÇÅ k.snd).natTrans.app (unop j) ‚â´ (pres.f k.fst.1).natTrans.app (unop j)) =
          E.p‚ÇÅ k.snd ‚â´ E.f k.fst.1 ‚â´ pres.pres.œÄ.app (unop j) by
        simp [h, ‚Üê Functor.map_comp, ‚Üê op_comp, PreOneHypercover.multifork]
      rw [‚Üê pres.hf, ‚Üê pres.hp‚ÇÅ]
      simp

section

variable (J : GrothendieckTopology C) (K : GrothendieckTopology D)

variable (F A) in
/--
The family of `1`-hypercovers of `D` (think: `= Pro C`) that is a limit
of `1`-hypercovers in `C`.

We shall show that if `D = Pro C`, the finite `1`-hypercovers for a suitable topology
on `D` satisfy this.
-/
def GrothendieckTopology.oneHypercoverRelativelyRepresentable [HasFilteredColimitsOfSize.{w, w} A] :
    K.OneHypercoverFamily :=
  fun _ E ‚Ü¶ ‚àÉ (I : Type w) (_ : SmallCategory I) (_ : IsCofiltered I)
    (pres : E.toPreOneHypercover.RelativeLimitPresentation I F),
    PreservesLimitsOfShape (WalkingMulticospan E.multicospanShape)
      (colim : (I·µí·µñ ‚•§ A) ‚•§ _) ‚àß
    ‚àÄ j, (pres.preOneHypercover j).sieve‚ÇÄ ‚àà J _ ‚àß
      ‚àÄ {a b : E.I‚ÇÄ} {W : C} (p‚ÇÅ : W ‚ü∂ (pres.preOneHypercover j).X a)
        (p‚ÇÇ : W ‚ü∂ (pres.preOneHypercover j).X b),
        p‚ÇÅ ‚â´ (pres.preOneHypercover j).f a = p‚ÇÇ ‚â´ (pres.preOneHypercover j).f b ‚Üí
        (pres.preOneHypercover j).sieve‚ÇÅ p‚ÇÅ p‚ÇÇ ‚àà J W

/--
Let `K` be a topology generated by limits of `1`-hypercovers in `C`. Then if `P` is a
presheaf on `D` (think: `= Pro C`) such that

1. `P` restricted to `C` is a sheaf, and
2. `P` preserves filtered colimits, then

`P` is a sheaf.
-/
lemma Presheaf.IsSheaf.of_preservesFilteredColimitsOfSize (P : D·µí·µñ ‚•§ A) (h : IsSheaf J (F.op ‚ãô P))
    [PreservesFilteredColimitsOfSize.{w, w} P] [HasFilteredColimitsOfSize.{w, w} A]
    [(GrothendieckTopology.oneHypercoverRelativelyRepresentable.{w} F A J K).IsGenerating] :
    IsSheaf K P := by
  rw [(GrothendieckTopology.oneHypercoverRelativelyRepresentable F A J K).isSheaf_iff]
  intro X E hE@‚ü®I, _, _, pres, _, mem‚ü©
  constructor
  apply pres.isLimit (P := P)
  intro j
  let E' : J.OneHypercover (pres.pres.diag.obj j) := pres.oneHypercover j (mem j).1 (mem j).2
  exact E'.isLimitMultifork ‚ü®_, h‚ü©

end

end CategoryTheory
