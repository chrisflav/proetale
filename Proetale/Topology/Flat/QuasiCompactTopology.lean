/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.AlgebraicGeometry.Sites.BigZariski
import Mathlib.CategoryTheory.Sites.Preserves
import Proetale.Mathlib.AlgebraicGeometry.Sites.MorphismProperty
import Proetale.Mathlib.AlgebraicGeometry.Extensive
import Proetale.Mathlib.CategoryTheory.Limits.Preserves.Finite
import Proetale.Mathlib.CategoryTheory.Sites.Sheaf
import Proetale.Mathlib.CategoryTheory.Sites.IsSheafFor
import Proetale.Topology.Flat.QuasiCompactCover
import Proetale.Mathlib.CategoryTheory.Extensive

/-!
# The quasi-compact topology of a scheme

The `qc`-pretopology of a scheme wrt. to a morphism property `P` is the pretopology
given by quasi compact covers satisfying `P`.

We show that a presheaf is a sheaf in this topology if and only if it is a sheaf
in the Zariski topology and a sheaf on single object `P`-coverings of affine schemes.
-/

universe v u

open CategoryTheory Limits Opposite

namespace CategoryTheory

variable {C : Type*} [Category C] {X : C}

-- this needs more assumptions, but the proof will show which the correct ones are
lemma Presieve.isSheafFor_ofArrows_comp {F : Cáµ’áµ– â¥¤ Type*} {Î¹ : Type*} {Y Z : Î¹ â†’ C}
    (f : âˆ€ i, Y i âŸ¶ X) (g : âˆ€ i, Z i âŸ¶ X)
    (e : âˆ€ i, Y i â‰… Z i) (H : Presieve.IsSheafFor F (.ofArrows _ g)) :
    Presieve.IsSheafFor F (.ofArrows _ (fun i â†¦ (e i).hom â‰« g i)) := by
  let B (W : C) (w : W âŸ¶ X) (hw : Presieve.ofArrows _ g w) : Sieve W :=
    sorry
  have : .ofArrows _ (fun i â†¦ (e i).hom â‰« g i) = Sieve.bind (.ofArrows _ g) B :=
    sorry
  rw [Presieve.isSheafFor_iff_generate, â† Sieve.ofArrows, this]
  sorry

end CategoryTheory

namespace AlgebraicGeometry

open Scheme

variable {P : MorphismProperty Scheme.{u}}

@[simp]
lemma Scheme.Cover.ofArrows_sigma {S : Scheme.{u}} (ğ’° : S.Cover P) [IsLocalAtSource P] :
    Presieve.ofArrows ğ’°.sigma.obj ğ’°.sigma.map = Presieve.singleton (Sigma.desc ğ’°.map) := by
  refine le_antisymm ?_ ?_
  Â· intro T g âŸ¨iâŸ©
    exact Presieve.singleton_self _
  Â· intro T g âŸ¨âŸ©
    exact âŸ¨âŸ¨âŸ©âŸ©

/-- The `qc`-pretopology of a scheme wrt. to a morphism property `P` is the pretopology
given by quasi compact covers satisfying `P`. -/
def qcPretopology (P : MorphismProperty Scheme.{u}) [P.IsMultiplicative]
    [P.IsStableUnderBaseChange] : Pretopology Scheme.{u} where
  coverings Y S := âˆƒ (ğ’° : Cover.{u} P Y) (h : ğ’°.QuasiCompact), S = Presieve.ofArrows ğ’°.obj ğ’°.map
  has_isos _ _ f _ := âŸ¨coverOfIsIso f, inferInstance, (Presieve.ofArrows_pUnit _).symmâŸ©
  pullbacks := by
    rintro Y X f _ âŸ¨ğ’°, hğ’°, rflâŸ©
    exact âŸ¨ğ’°.pullbackCover' f, inferInstance, (Presieve.ofArrows_pullback _ _ _).symmâŸ©
  transitive := by
    rintro X _ T âŸ¨ğ’°, hğ’°, rflâŸ© H
    choose ğ’± hcğ’± hğ’± using H
    refine âŸ¨ğ’°.bind (fun j â†¦ ğ’± (ğ’°.map j) âŸ¨jâŸ©), inferInstance, ?_âŸ©
    simpa only [Cover.bind, â† hğ’±] using Presieve.ofArrows_bind ğ’°.obj ğ’°.map _
      (fun _ f H => (ğ’± f H).obj) (fun _ f H => (ğ’± f H).map)

abbrev qcTopology (P : MorphismProperty Scheme.{u}) [P.IsMultiplicative]
    [P.IsStableUnderBaseChange] : GrothendieckTopology Scheme.{u} := (qcPretopology P).toGrothendieck

@[simp]
lemma Scheme.Hom.singleton_mem_qcPretopology [P.IsMultiplicative] [P.IsStableUnderBaseChange]
    {X Y : Scheme.{u}} {f : X âŸ¶ Y} (hf : P f) [Surjective f] [QuasiCompact f] :
    Presieve.singleton f âˆˆ qcPretopology P Y := by
  refine âŸ¨f.cover hf, inferInstance, ?_âŸ©
  rw [ofArrows_homCover]

@[simp]
lemma Scheme.Hom.generate_singleton_mem_qcTopology [P.IsMultiplicative] [P.IsStableUnderBaseChange]
    {X Y : Scheme.{u}} (f : X âŸ¶ Y) (hf : P f) [Surjective f] [QuasiCompact f] :
    Sieve.generate (Presieve.singleton f) âˆˆ qcTopology P Y := by
  refine âŸ¨Presieve.singleton f, ?_, ?_âŸ©
  Â· exact f.singleton_mem_qcPretopology hf
  Â· exact Sieve.le_generate _

@[simp]
lemma Scheme.Cover.generate_ofArrows_mem_qcTopology [P.IsMultiplicative]
    [P.IsStableUnderBaseChange] {S : Scheme.{u}} (ğ’° : Cover.{u} P S) [ğ’°.QuasiCompact] :
    .generate (.ofArrows ğ’°.obj ğ’°.map) âˆˆ qcTopology P S := by
  rw [qcTopology, Pretopology.mem_toGrothendieck]
  exact âŸ¨.ofArrows ğ’°.obj ğ’°.map, âŸ¨ğ’°, â€¹_â€º, rflâŸ©, Sieve.le_generate _âŸ©

-- This holds more generally if `ğ’°.J` is `u`-small, but we don't need that for now.
lemma Scheme.Cover.isSheafFor_sigma_iff {F : Scheme.{u}áµ’áµ– â¥¤ Type*} [IsLocalAtSource P]
    (hF : Presieve.IsSheaf Scheme.zariskiTopology F)
    {S : Scheme.{u}} (ğ’° : S.Cover P) [Finite ğ’°.J] :
    Presieve.IsSheafFor F (.ofArrows ğ’°.sigma.obj ğ’°.sigma.map) â†”
      Presieve.IsSheafFor F (.ofArrows ğ’°.obj ğ’°.map) := by
  have : PreservesFiniteProducts F := preservesFiniteProducts_of_isSheaf_zariskiTopology hF
  conv_rhs => rw [â† Presieve.isSheafFor_sigmaDesc_iff]
  congr!
  rw [Scheme.Cover.ofArrows_sigma]

variable (P : MorphismProperty Scheme.{u}) [P.IsMultiplicative] [P.IsStableUnderBaseChange]

lemma zariskiTopology_le_qcTopology [IsLocalAtSource P] :
    zariskiTopology â‰¤ qcTopology P := by
  rw [qcTopology, zariskiTopology, (Pretopology.gi _).gc.le_iff_le]
  rintro S R âŸ¨ğ’°, rflâŸ©
  rw [Pretopology.mem_ofGrothendieck]
  let ğ’°' : Cover P S := ğ’°.changeProp P (fun j â†¦ IsLocalAtSource.of_isOpenImmersion _)
  have : ğ’°'.QuasiCompact := âŸ¨(inferInstanceAs <| ğ’°.QuasiCompact).1âŸ©
  exact ğ’°'.generate_ofArrows_mem_qcTopology

open Opposite

@[simps!]
noncomputable
def Scheme.affineCover' (X : Scheme.{u}) : X.OpenCover :=
  .mkOfCovers X.affineOpens (fun i â†¦ i.1) (fun i â†¦ i.1.Î¹) fun x â†¦ by
    obtain âŸ¨U, hU, hx, -âŸ© := TopologicalSpace.Opens.isBasis_iff_nbhd.mp (isBasis_affine_open X)
      (show x âˆˆ âŠ¤ from trivial)
    exact âŸ¨âŸ¨U, hUâŸ©, âŸ¨x, hxâŸ©, rflâŸ©

variable {P}

lemma Scheme.Cover.Hom.isSheafFor {F : Scheme.{u}áµ’áµ– â¥¤ Type*} {S : Scheme.{u}} {ğ’° ğ’± : S.Cover P}
    (f : ğ’° âŸ¶ ğ’±)
    (Hâ‚ : Presieve.IsSheafFor F (.ofArrows _ ğ’°.map))
    (Hâ‚‚ : âˆ€ {X : Scheme.{u}} (f : X âŸ¶ S),
      Presieve.IsSeparatedFor F (.ofArrows (ğ’°.pullbackCover' f).obj (ğ’°.pullbackCover' f).map)) :
    Presieve.IsSheafFor F (.ofArrows ğ’±.obj ğ’±.map) := by
  rw [Presieve.isSheafFor_iff_generate]
  apply Presieve.isSheafFor_subsieve_aux (S := .generate (.ofArrows ğ’°.obj ğ’°.map))
  Â· show Sieve.generate _ â‰¤ Sieve.generate _
    rw [Sieve.generate_le_iff]
    rintro - - âŸ¨iâŸ©
    rw [â† f.w]
    exact âŸ¨_, f.app i, ğ’±.map _, âŸ¨_âŸ©, rflâŸ©
  Â· rwa [â† Presieve.isSheafFor_iff_generate]
  Â· intro Y f hf
    rw [â† Sieve.pullbackArrows_comm, â† Presieve.isSeparatedFor_iff_generate]
    rw [â† Presieve.ofArrows_pullback]
    apply Hâ‚‚

lemma isSheafFor_iff_of_iso {F : Scheme.{u}áµ’áµ– â¥¤ Type*} {S X Y : Scheme.{u}} (f : X âŸ¶ S) (g : Y âŸ¶ S)
    (e : X â‰… Y) (hF : Presieve.IsSheaf Scheme.zariskiTopology F)
    (he : e.hom â‰« g = f) :
    Presieve.IsSheafFor F (.singleton f) â†” Presieve.IsSheafFor F (.singleton g) := by
  subst he
  refine âŸ¨fun hf â†¦ ?_, ?_âŸ©
  Â· sorry
  Â· sorry

/-- A pre-sheaf is a sheaf for the `P`-qc topology if and only if it is a sheaf
for the Zariski topology and satisfies the sheaf property for all single object coverings
`{ f : Spec S âŸ¶ Spec R }` where `f` satisifies `P`.-/
@[stacks 022H]
nonrec lemma isSheaf_qcTopology_iff (F : Scheme.{u}áµ’áµ– â¥¤ Type*) [IsLocalAtSource P] :
    Presieve.IsSheaf (qcTopology P) F â†”
      Presieve.IsSheaf Scheme.zariskiTopology F âˆ§
        âˆ€ {R S : CommRingCat.{u}} (f : R âŸ¶ S), P (Spec.map f) â†’ Surjective (Spec.map f) â†’
          Presieve.IsSheafFor F (.singleton (Spec.map f)) := by
  refine âŸ¨fun hF â†¦ âŸ¨?_, fun {R S} f hf hs â†¦ ?_âŸ©, fun âŸ¨hzar, hffâŸ© â†¦ ?_âŸ©
  Â· exact Presieve.isSheaf_of_le _ (zariskiTopology_le_qcTopology P) hF
  Â· apply hF.isSheafFor
    rw [â† ofArrows_homCover _ hf]
    exact Cover.generate_ofArrows_mem_qcTopology _
  Â· rw [Presieve.isSheaf_pretopology]
    rintro T - âŸ¨ğ’°, _, rflâŸ©
    wlog hT : âˆƒ (R : CommRingCat.{u}), T = Spec R generalizing T
    Â· let ğ’± : T.OpenCover := T.affineCover
      have h (j : T.affineCover.J) : Presieve.IsSheafFor F
          (.ofArrows (ğ’°.pullbackCover' (ğ’±.map j)).obj (ğ’°.pullbackCover' (ğ’±.map j)).map) :=
        this _ inferInstance âŸ¨_, rflâŸ©
      refine .of_isSheafFor_pullback' F (.ofArrows ğ’±.obj ğ’±.map) _ ?_ ?_ ?_ ?_
      Â· exact hzar.isSheafFor _ _ ğ’±.generate_ofArrows_mem_grothendieckTopology
      Â· intro Y f
        refine (hzar.isSheafFor _ _ ?_).isSeparatedFor
        rw [Sieve.generate_sieve, â† Sieve.pullbackArrows_comm, Cover.pullbackArrows_ofArrows]
        exact (Cover.pullbackCover' ğ’± f).generate_ofArrows_mem_grothendieckTopology
      Â· rintro - - - - âŸ¨iâŸ© âŸ¨jâŸ©
        use .ofArrows (pullback (ğ’±.map i) (ğ’±.map j)).affineCover.obj
          (pullback (ğ’±.map i) (ğ’±.map j)).affineCover.map
        refine âŸ¨(hzar.isSheafFor _ _ <|
            Cover.generate_ofArrows_mem_grothendieckTopology _).isSeparatedFor, ?_âŸ©
        Â· rintro - - âŸ¨kâŸ©
          rw [â† Sieve.pullbackArrows_comm, â† Presieve.isSeparatedFor_iff_generate]
          apply Presieve.IsSheafFor.isSeparatedFor
          rw [â† Presieve.ofArrows_pullback]
          exact this (ğ’°.pullbackCover' _) inferInstance âŸ¨_, rflâŸ©
      Â· rintro - - âŸ¨iâŸ©
        rw [â† Sieve.pullbackArrows_comm, â† Presieve.ofArrows_pullback,
          â† Presieve.isSheafFor_iff_generate]
        exact this (ğ’°.pullbackCover' (ğ’±.map i)) inferInstance âŸ¨_, rflâŸ©
    obtain âŸ¨R, rflâŸ© := hT
    wlog hğ’° : (âˆ€ i, IsAffine (ğ’°.obj i)) âˆ§ Finite ğ’°.J generalizing R ğ’°
    Â· obtain âŸ¨ğ’±, f, hfin, hoâŸ© := Cover.QuasiCompact.exists_hom ğ’°
      have H (V : Scheme.{u}) (f : V âŸ¶ Spec R) : Presieve.IsSheafFor F
          (.ofArrows (ğ’±.cover.pullbackCover' f).obj (ğ’±.cover.pullbackCover' f).map) := by
        let ğ’°V := V.affineCover
        refine .of_isSheafFor_pullback' F (.ofArrows ğ’°V.obj ğ’°V.map) _ ?_ ?_ ?_ ?_
        Â· exact hzar.isSheafFor _ _ ğ’°V.generate_ofArrows_mem_grothendieckTopology
        Â· intro Y f
          refine (hzar.isSheafFor _ _ ?_).isSeparatedFor
          rw [Sieve.generate_sieve, â† Sieve.pullbackArrows_comm, Cover.pullbackArrows_ofArrows]
          exact (Cover.pullbackCover' ğ’°V f).generate_ofArrows_mem_grothendieckTopology
        Â· rintro - - - - âŸ¨iâŸ© âŸ¨jâŸ©
          refine âŸ¨.ofArrows _ (pullback (ğ’°V.map i) (ğ’°V.map j)).affineCover.map, ?_, ?_âŸ©
          Â· exact hzar.isSheafFor _ _
              (Cover.generate_ofArrows_mem_grothendieckTopology _) |>.isSeparatedFor
          Â· rintro - - âŸ¨kâŸ©
            rw [â† Sieve.pullbackArrows_comm, â† Presieve.ofArrows_pullback,
              â† Presieve.isSeparatedFor_iff_generate]
            refine (this _ ((ğ’±.cover.pullbackCover' f).pullbackCover' _) inferInstance
              âŸ¨fun l â†¦ ?_, hfinâŸ©).isSeparatedFor
            exact .of_isIso (pullbackLeftPullbackSndIso (ğ’±.map l) f _).hom
        Â· rintro - - âŸ¨iâŸ©
          rw [â† Sieve.pullbackArrows_comm, â† Presieve.ofArrows_pullback,
            â† Presieve.isSheafFor_iff_generate]
          let ğ’°' := (ğ’±.cover.pullbackCover' f).pullbackCover' (ğ’°V.map i)
          refine this _ ğ’°' inferInstance
            âŸ¨fun j â†¦ .of_isIso (pullbackLeftPullbackSndIso (ğ’±.map j) f (ğ’°V.map i)).hom, hfinâŸ©
      refine f.isSheafFor ?_ fun f â†¦ (H _ f).isSeparatedFor
      exact this _ _ inferInstance âŸ¨fun i â†¦ inferInstanceAs <| IsAffine (Spec _), hfinâŸ©
    obtain âŸ¨_, _âŸ© := hğ’°
    let ğ’°' := ğ’°.sigma
    rw [â† Scheme.Cover.isSheafFor_sigma_iff hzar, Scheme.Cover.ofArrows_of_unique]
    have : IsAffine (ğ’°.sigma.obj default) := by dsimp; infer_instance
    let f : Spec _ âŸ¶ Spec R := (ğ’°.sigma.obj default).isoSpec.inv â‰« ğ’°.sigma.map default
    obtain âŸ¨Ï†, hÏ†âŸ© := Spec.map_surjective f
    rw [isSheafFor_iff_of_iso _ (Spec.map Ï†) (ğ’°.sigma.obj default).isoSpec hzar (by simp [hÏ†, f])]
    refine hff _ ?_ ?_
    Â· simpa only [hÏ†, f] using IsLocalAtSource.comp (ğ’°.sigma.map_prop _) _
    Â· simp only [hÏ†, f, Cover.sigma_J, PUnit.default_eq_unit, Cover.sigma_obj, Cover.sigma_map, f]
      infer_instance

end AlgebraicGeometry
