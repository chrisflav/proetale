/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib
import Proetale.FromPi1.Etale
import Proetale.Mathlib.AlgebraicGeometry.Extensive
import Proetale.Mathlib.CategoryTheory.Limits.MorphismProperty
import Proetale.Mathlib.CategoryTheory.Limits.Constructions.Over.Connected
import Proetale.Mathlib.CategoryTheory.Limits.Comma
import Proetale.Topology.Flat.Sheaf
import Proetale.Topology.Coherent.Etale

/-!
# Affine √©tale site
-/

universe u

open CategoryTheory Opposite Limits

namespace CategoryTheory

section

variable {C D : Type*} [Category C] [Category D] (F : C ‚•§ D) (X : D)

instance (J : Type*) [Category J] [IsConnected J] [HasLimitsOfShape J C]
    [PreservesLimitsOfShape J F]
    [HasLimitsOfShape J D] :
    PreservesLimitsOfShape J (CostructuredArrow.toOver F X) where
  preservesLimit {K} := by
    have : PreservesLimitsOfShape J (CostructuredArrow.proj F X) := by
      infer_instance
    have : PreservesLimit K (CostructuredArrow.toOver F X ‚ãô Over.forget X) := by
      show PreservesLimit K (CostructuredArrow.proj F X ‚ãô F)
      infer_instance
    have : HasLimit (K ‚ãô CostructuredArrow.toOver F X) := by
      infer_instance
    have : PreservesLimit (K ‚ãô CostructuredArrow.toOver F X) (Over.forget X) := by
      infer_instance
    have : ReflectsLimit (K ‚ãô CostructuredArrow.toOver F X) (Over.forget X) :=
      reflectsLimit_of_reflectsIsomorphisms _ _
    apply Limits.preservesLimit_of_reflects_of_preserves _ (Over.forget X)

end

namespace MorphismProperty

variable {C D : Type*} [Category C] [Category D]
variable (P : MorphismProperty D) (Q : MorphismProperty C) [Q.IsMultiplicative] (F : C ‚•§ D) (X : D)

protected abbrev CostructuredArrow (P : MorphismProperty D) (Q : MorphismProperty C) (F : C ‚•§ D) (X : D) :=
  P.Comma F (Functor.fromPUnit.{0} X) Q ‚ä§

namespace CostructuredArrow

variable {P F X} in
@[simps left hom]
protected def mk {A : C} (f : F.obj A ‚ü∂ X) (hf : P f) : P.CostructuredArrow Q F X where
  left := A
  right := ‚ü®‚ü®‚ü©‚ü©
  hom := f
  prop := hf

variable {P Q F X} in
@[simps left]
def homMk {A B : P.CostructuredArrow Q F X} (f : A.left ‚ü∂ B.left) (hf : Q f)
    (w : F.map f ‚â´ B.hom = A.hom := by cat_disch) :
    A ‚ü∂ B where
  left := f
  right := eqToHom (Subsingleton.elim _ _)
  prop_hom_left := hf
  prop_hom_right := trivial

variable {P Q F X} in
@[ext]
lemma Hom.ext {A B : P.CostructuredArrow Q F X} {f g : A ‚ü∂ B} (h : f.left = g.left) :
    f = g := by
  ext <;> simp [h]

protected abbrev forget :
    P.CostructuredArrow Q F X ‚•§ CategoryTheory.CostructuredArrow F X :=
  Comma.forget _ _ _ _ _

@[simps]
protected def toOver :
    P.CostructuredArrow ‚ä§ F X ‚•§ P.Over ‚ä§ X where
  obj Y := Over.mk _ Y.hom Y.prop
  map f := Over.homMk (F.map f.left) _

instance [F.Faithful] : (CostructuredArrow.toOver P F X).Faithful := by
  constructor
  intro A B f g hfg
  ext
  exact F.map_injective congr($(hfg).left)

instance [F.Full] : (CostructuredArrow.toOver P F X).Full := by
  constructor
  intro A B f
  refine ‚ü®homMk (F.preimage f.left) trivial ?_, ?_‚ü©
  ¬∑ simpa using f.w
  ¬∑ ext; simp

variable [P.IsStableUnderBaseChange]
  [P.IsStableUnderComposition] [P.HasOfPostcompProperty P]
  [PreservesLimitsOfShape WalkingCospan F] [HasPullbacks C] [HasPullbacks D]

lemma closedUnderLimitsOfShape_walkingCospan :
    ClosedUnderLimitsOfShape WalkingCospan (fun f : CostructuredArrow F X ‚Ü¶ P f.hom) := by
  intro K c hc hf
  have h : IsPullback (F.map (c.œÄ.app .left).left) (F.map (c.œÄ.app .right).left)
      (F.map (K.map WalkingCospan.Hom.inl).left) (F.map (K.map WalkingCospan.Hom.inr).left) :=
    IsPullback.of_isLimit_cone <| isLimitOfPreserves
      (CategoryTheory.CostructuredArrow.toOver F X ‚ãô CategoryTheory.Over.forget X) hc
  rw [show c.pt.hom = F.map (c.œÄ.app .left).left ‚â´ (K.obj .left).hom by simp]
  apply P.comp_mem _ _ (P.of_isPullback h.flip ?_) (hf _)
  exact P.of_postcomp _ (K.obj WalkingCospan.one).hom (hf .one) (by simpa using hf .right)

noncomputable
instance createsLimitsOfShape_walkingCospan [HasPullbacks C] [HasPullbacks D] :
    CreatesLimitsOfShape WalkingCospan (CostructuredArrow.forget P ‚ä§ F X) :=
  haveI : HasLimitsOfShape WalkingCospan (Comma F (Functor.fromPUnit X)) :=
    inferInstanceAs <| HasLimitsOfShape WalkingCospan (CostructuredArrow F X)
  Comma.forgetCreatesLimitsOfShapeOfClosed P
    (closedUnderLimitsOfShape_walkingCospan P F X)

instance hasPullbacks : HasPullbacks (P.CostructuredArrow ‚ä§ F X) :=
  haveI : HasLimitsOfShape WalkingCospan (Comma F (Functor.fromPUnit X)) :=
    inferInstanceAs <| HasLimitsOfShape WalkingCospan (CostructuredArrow F X)
  Comma.hasLimitsOfShape_of_closedUnderLimitsOfShape P
    (closedUnderLimitsOfShape_walkingCospan P F X)

instance : PreservesLimitsOfShape WalkingCospan (CostructuredArrow.toOver P F X) := by
  have : PreservesLimitsOfShape WalkingCospan
      (CostructuredArrow.toOver P F X ‚ãô Over.forget P ‚ä§ X) := by
    show PreservesLimitsOfShape WalkingCospan <|
      CostructuredArrow.forget P ‚ä§ F X ‚ãô CategoryTheory.CostructuredArrow.toOver F X
    infer_instance
  exact preservesLimitsOfShape_of_reflects_of_preserves _ (Over.forget _ _ X)

end CostructuredArrow

end MorphismProperty

end CategoryTheory

namespace AlgebraicGeometry

namespace Scheme

variable {S : Scheme.{u}}

section

@[simps! hom left]
def affineOverMk {P : MorphismProperty Scheme.{u}} {R : CommRingCat.{u}}
    (f : Spec R ‚ü∂ S) (hf : P f) :
    P.CostructuredArrow ‚ä§ Scheme.Spec S :=
  .mk ‚ä§ f hf

instance isCoverDense_toOver_Spec (P : MorphismProperty Scheme.{u}) [P.IsMultiplicative]
    [IsLocalAtSource P] [P.IsStableUnderBaseChange] [P.HasOfPostcompProperty P] :
    (MorphismProperty.CostructuredArrow.toOver P Scheme.Spec S).IsCoverDense
      (smallGrothendieckTopology P) where
  is_cover U := by
    rw [Scheme.mem_smallGrothendieckTopology]
    let ùí∞ : Cover.{u} P U.left := U.left.affineCover.changeProp _
      (fun _ ‚Ü¶ IsLocalAtSource.of_isOpenImmersion _)
    let _ (i : ùí∞.J) : (ùí∞.obj i).Over S := ‚ü®ùí∞.map i ‚â´ U.hom‚ü©
    refine ‚ü®ùí∞, ?_, ?_, ?_‚ü©
    ¬∑ exact ‚ü®fun i ‚Ü¶ inferInstance, fun i ‚Ü¶ ‚ü®rfl‚ü©‚ü©
    ¬∑ intro i
      apply P.comp_mem
      exact ùí∞.map_prop i
      exact U.prop
    ¬∑ rintro X f ‚ü®i‚ü©
      simp only [Sieve.coverByImage]
      refine ‚ü®‚ü®affineOverMk (ùí∞.map i ‚â´ U.hom) (P.comp_mem _ _ (ùí∞.map_prop i) U.prop), ?_, ?_, ?_‚ü©‚ü©
      ¬∑ exact MorphismProperty.CostructuredArrow.homMk (ùüô _) ‚ü®‚ü© rfl
      ¬∑ dsimp
        exact MorphismProperty.Over.homMk (ùí∞.map i) (by simp) trivial
      ¬∑ ext
        simp

variable {P : MorphismProperty Scheme.{u}} [P.IsMultiplicative]
  [IsLocalAtSource P] [P.IsStableUnderBaseChange] [P.HasOfPostcompProperty P]

instance : HasFiniteCoproducts (P.CostructuredArrow ‚ä§ Scheme.Spec S) :=
  sorry

instance : PreservesColimitsOfShape (Discrete WalkingPair)
    (MorphismProperty.CostructuredArrow.toOver P Scheme.Spec S) :=
  sorry

instance : FinitaryExtensive (P.CostructuredArrow ‚ä§ Scheme.Spec S) :=
  CategoryTheory.finitaryExtensive_of_preserves_and_reflects_isomorphism
    (MorphismProperty.CostructuredArrow.toOver P Scheme.Spec S)

instance : Preregular (P.CostructuredArrow ‚ä§ Scheme.Spec S) := by
  apply Preregular.of_hasPullbacks_of_effectiveEpi_fst
  sorry

end

noncomputable
def Cover.etaleAffineRefinement (ùí∞ : S.Cover @IsEtale) : S.AffineCover @IsEtale where
  J := (ùí∞.bind fun j ‚Ü¶ (ùí∞.obj j).affineCover.changeProp _ (fun _ ‚Ü¶ inferInstance)).J
  obj _ := _
  map := (ùí∞.bind fun j => (ùí∞.obj j).affineCover.changeProp _ fun _ ‚Ü¶ inferInstance).map
  f := (ùí∞.bind fun j => (ùí∞.obj j).affineCover.changeProp _ fun _ ‚Ü¶ inferInstance).f
  covers := (ùí∞.bind fun j => (ùí∞.obj j).affineCover.changeProp _ fun _ ‚Ü¶ inferInstance).covers
  map_prop j := by
    simp [Cover.changeProp]
    have : IsEtale (ùí∞.map j.fst) := ùí∞.map_prop _
    infer_instance

def AffineEtale (S : Scheme.{u}) : Type (u + 1) :=
  MorphismProperty.CostructuredArrow @IsEtale.{u} ‚ä§ Scheme.Spec.{u} S

namespace AffineEtale

@[simps!]
protected def mk {R : CommRingCat} (f : Spec R ‚ü∂ S) [IsEtale f] : AffineEtale S :=
  MorphismProperty.CostructuredArrow.mk ‚ä§ f ‚Äπ_‚Ä∫

instance : Category S.AffineEtale :=
  inferInstanceAs <| Category (MorphismProperty.CostructuredArrow _ _ _ _)

@[simps! obj_left obj_hom map_left]
protected noncomputable def Spec (S : Scheme.{u}) : S.AffineEtale ‚•§ S.Etale :=
  MorphismProperty.CostructuredArrow.toOver _ _ _

instance : (AffineEtale.Spec S).Faithful :=
  inferInstanceAs <| (MorphismProperty.CostructuredArrow.toOver _ _ _).Faithful

instance : (AffineEtale.Spec S).Full :=
  inferInstanceAs <| (MorphismProperty.CostructuredArrow.toOver _ _ _).Full

instance : (AffineEtale.Spec S).IsCoverDense S.smallEtaleTopology :=
  inferInstanceAs <| (MorphismProperty.CostructuredArrow.toOver _ _ _).IsCoverDense
    (smallGrothendieckTopology _)

variable (S) in
def topology : GrothendieckTopology S.AffineEtale :=
  (AffineEtale.Spec S).inducedTopology (smallEtaleTopology S)

/-- The category of sheafs on the small, affine √©tale site is equivalent to the category of
sheafs on the small √©tale site. -/
noncomputable def sheafEquiv (A : Type*) [Category A]
    [‚àÄ (X : S.Etale·µí·µñ), Limits.HasLimitsOfShape (StructuredArrow X (AffineEtale.Spec S).op) A] :
    Sheaf (AffineEtale.topology S) A ‚âå Sheaf (smallEtaleTopology S) A :=
  (AffineEtale.Spec S).sheafInducedTopologyEquivOfIsCoverDense _ _

instance : (AffineEtale.Spec S).ReflectsEffectiveEpis :=
  inferInstanceAs <| (MorphismProperty.CostructuredArrow.toOver _ _ _).ReflectsEffectiveEpis

instance effectiveEpi_of_surjective {S : Scheme} {X Y : S.AffineEtale} (f : X ‚ü∂ Y)
    [Surjective (Spec.map f.left.unop)] : EffectiveEpi f := by
  apply (AffineEtale.Spec S).effectiveEpi_of_map
  have : Surjective ((AffineEtale.Spec S).map f).left := ‚Äπ_‚Ä∫
  infer_instance

instance : HasPullbacks S.AffineEtale :=
  inferInstanceAs <| HasPullbacks (MorphismProperty.CostructuredArrow _ _ _ _)

-- Question: What are the effective epimorphisms of `AffineEtale S`?

instance preregular : Preregular (AffineEtale S) := by
  sorry

instance precoherent : Precoherent (AffineEtale S) :=
  sorry

end AffineEtale

end Scheme

end AlgebraicGeometry
