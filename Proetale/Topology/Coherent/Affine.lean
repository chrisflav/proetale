/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib
import Proetale.FromPi1.Etale
import Proetale.Mathlib.AlgebraicGeometry.Extensive
import Proetale.Mathlib.CategoryTheory.Limits.MorphismProperty
import Proetale.Mathlib.CategoryTheory.Limits.Constructions.Over.Connected
import Proetale.Mathlib.CategoryTheory.Limits.Comma
import Proetale.Topology.Flat.Sheaf
import Proetale.Topology.Coherent.Etale

/-!
# Affine √©tale site
-/

universe u

open CategoryTheory Opposite Limits

namespace CategoryTheory

section

variable {C D : Type*} [Category C] [Category D] (F : C ‚•§ D) (X : D)

instance (J : Type*) [Category J] [IsConnected J] [HasLimitsOfShape J C]
    [PreservesLimitsOfShape J F]
    [HasLimitsOfShape J D] :
    PreservesLimitsOfShape J (CostructuredArrow.toOver F X) where
  preservesLimit {K} := by
    have : PreservesLimitsOfShape J (CostructuredArrow.proj F X) := by
      infer_instance
    have : PreservesLimit K (CostructuredArrow.toOver F X ‚ãô Over.forget X) := by
      show PreservesLimit K (CostructuredArrow.proj F X ‚ãô F)
      infer_instance
    have : HasLimit (K ‚ãô CostructuredArrow.toOver F X) := by
      infer_instance
    have : PreservesLimit (K ‚ãô CostructuredArrow.toOver F X) (Over.forget X) := by
      infer_instance
    have : ReflectsLimit (K ‚ãô CostructuredArrow.toOver F X) (Over.forget X) :=
      reflectsLimit_of_reflectsIsomorphisms _ _
    apply Limits.preservesLimit_of_reflects_of_preserves _ (Over.forget X)

end

namespace MorphismProperty

variable {C D : Type*} [Category C] [Category D]
variable (P : MorphismProperty D) (Q : MorphismProperty C) [Q.IsMultiplicative] (F : C ‚•§ D) (X : D)

namespace CostructuredArrow

variable [P.IsStableUnderBaseChange]
  [P.IsStableUnderComposition] [P.HasOfPostcompProperty P]
  [PreservesLimitsOfShape WalkingCospan F] [HasPullbacks C] [HasPullbacks D]

lemma closedUnderLimitsOfShape_walkingCospan :
    (P.costructuredArrowObj F (X := X)).IsClosedUnderLimitsOfShape WalkingCospan where
  limitsOfShape_le := by
    rintro Y ‚ü®pres, hpres‚ü©
    have h : IsPullback (F.map (pres.œÄ.app .left).left) (F.map (pres.œÄ.app .right).left)
        (F.map (pres.diag.map WalkingCospan.Hom.inl).left)
          (F.map (pres.diag.map WalkingCospan.Hom.inr).left) :=
      IsPullback.of_isLimit_cone <| isLimitOfPreserves
        (CategoryTheory.CostructuredArrow.toOver F X ‚ãô CategoryTheory.Over.forget X) pres.isLimit
    rw [costructuredArrowObj_iff]
    rw [show Y.hom = F.map (pres.œÄ.app .left).left ‚â´ (pres.diag.obj .left).hom by simp]
    apply P.comp_mem _ _ (P.of_isPullback h.flip ?_) (hpres _)
    exact P.of_postcomp _ (pres.diag.obj WalkingCospan.one).hom (hpres .one)
      (by simpa using hpres .right)

noncomputable
instance createsLimitsOfShape_walkingCospan [HasPullbacks C] [HasPullbacks D] :
    CreatesLimitsOfShape WalkingCospan (CostructuredArrow.forget P ‚ä§ F X) :=
  haveI : HasLimitsOfShape WalkingCospan (Comma F (Functor.fromPUnit X)) :=
    inferInstanceAs <| HasLimitsOfShape WalkingCospan (CostructuredArrow F X)
  have : (commaObj F (Functor.fromPUnit X) P).IsClosedUnderLimitsOfShape WalkingCospan := by
    apply closedUnderLimitsOfShape_walkingCospan
  Comma.forgetCreatesLimitsOfShapeOfClosed P

instance hasPullbacks : HasPullbacks (P.CostructuredArrow ‚ä§ F X) :=
  haveI : HasLimitsOfShape WalkingCospan (Comma F (Functor.fromPUnit X)) :=
    inferInstanceAs <| HasLimitsOfShape WalkingCospan (CostructuredArrow F X)
  have : (commaObj F (Functor.fromPUnit X) P).IsClosedUnderLimitsOfShape WalkingCospan := by
    apply closedUnderLimitsOfShape_walkingCospan
  Comma.hasLimitsOfShape_of_closedUnderLimitsOfShape P

instance : PreservesLimitsOfShape WalkingCospan (CostructuredArrow.toOver P F X) := by
  have : PreservesLimitsOfShape WalkingCospan
      (CostructuredArrow.toOver P F X ‚ãô Over.forget P ‚ä§ X) := by
    show PreservesLimitsOfShape WalkingCospan <|
      CostructuredArrow.forget P ‚ä§ F X ‚ãô CategoryTheory.CostructuredArrow.toOver F X
    infer_instance
  exact preservesLimitsOfShape_of_reflects_of_preserves _ (Over.forget _ _ X)

end CostructuredArrow

end MorphismProperty

end CategoryTheory

namespace AlgebraicGeometry

namespace Scheme

variable {S : Scheme.{u}}

section

@[simps! hom left]
def affineOverMk {P : MorphismProperty Scheme.{u}} {R : CommRingCat.{u}}
    (f : Spec R ‚ü∂ S) (hf : P f) :
    P.CostructuredArrow ‚ä§ Scheme.Spec S :=
  .mk ‚ä§ f hf

instance isCoverDense_toOver_Spec (P : MorphismProperty Scheme.{u}) [P.IsMultiplicative]
    [IsZariskiLocalAtSource P] [P.IsStableUnderBaseChange] [P.HasOfPostcompProperty P] :
    (MorphismProperty.CostructuredArrow.toOver P Scheme.Spec S).IsCoverDense
      (smallGrothendieckTopology P) where
  is_cover U := by
    rw [Scheme.mem_smallGrothendieckTopology]
    let ùí∞ : Cover.{u} (precoverage P) U.left :=
      U.left.affineCover.changeProp
      (fun _ ‚Ü¶ IsZariskiLocalAtSource.of_isOpenImmersion _)
    let _ (i : ùí∞.I‚ÇÄ) : (ùí∞.X i).Over S := ‚ü®ùí∞.f i ‚â´ U.hom‚ü©
    refine ‚ü®ùí∞, ?_, ?_, ?_‚ü©
    ¬∑ exact ‚ü®fun i ‚Ü¶ inferInstance, fun i ‚Ü¶ ‚ü®rfl‚ü©‚ü©
    ¬∑ intro i
      apply P.comp_mem
      exact ùí∞.map_prop i
      exact U.prop
    ¬∑ rintro X f ‚ü®i‚ü©
      simp only [Sieve.coverByImage]
      refine ‚ü®‚ü®affineOverMk (ùí∞.f i ‚â´ U.hom) (P.comp_mem _ _ (ùí∞.map_prop i) U.prop), ?_, ?_, ?_‚ü©‚ü©
      ¬∑ exact MorphismProperty.CostructuredArrow.homMk (ùüô _) ‚ü®‚ü© rfl
      ¬∑ dsimp
        exact MorphismProperty.Over.homMk (ùí∞.f i) (by simp) trivial
      ¬∑ ext
        simp

variable {P : MorphismProperty Scheme.{u}} [IsZariskiLocalAtSource P]

instance IsZariskiLocalAtSource.isClosedUnderColimitsOfShape_discrete
    {Œπ : Type*} [Small.{u} Œπ] {C : Type*} [Category C] [HasColimitsOfShape (Discrete Œπ) C]
    (L : C ‚•§ Scheme.{u}) [PreservesColimitsOfShape (Discrete Œπ) L] (X : Scheme.{u}) :
    (P.costructuredArrowObj L (X := X)).IsClosedUnderColimitsOfShape (Discrete Œπ) := by
  refine CostructuredArrow.isClosedUnderColimitsOfShape ?_ ?_ ?_ _
  ¬∑ intro D _
    exact Sigma.cocone _
  ¬∑ intro D
    exact coproductIsCoproduct' _
  ¬∑ intro D _ X s h
    exact IsZariskiLocalAtSource.sigmaDesc (h ‚ü®¬∑‚ü©)

variable [P.IsStableUnderBaseChange] [P.HasOfPostcompProperty P] [P.IsMultiplicative]

instance : HasFiniteCoproducts (P.CostructuredArrow ‚ä§ Scheme.Spec S) where
  out n := by
    have : (MorphismProperty.commaObj Scheme.Spec (.fromPUnit S) P).IsClosedUnderColimitsOfShape
        (Discrete (Fin n)) := by
      apply IsZariskiLocalAtSource.isClosedUnderColimitsOfShape_discrete
    apply MorphismProperty.Comma.hasColimitsOfShape_of_closedUnderColimitsOfShape

instance : PreservesColimitsOfShape (Discrete WalkingPair)
    (MorphismProperty.CostructuredArrow.toOver P Scheme.Spec S) :=
  sorry

instance : FinitaryExtensive (P.CostructuredArrow ‚ä§ Scheme.Spec S) :=
  CategoryTheory.finitaryExtensive_of_preserves_and_reflects_isomorphism
    (MorphismProperty.CostructuredArrow.toOver P Scheme.Spec S)

instance : Preregular (P.CostructuredArrow ‚ä§ Scheme.Spec S) := by
  apply Preregular.of_hasPullbacks_of_effectiveEpi_fst
  sorry

end

noncomputable
def Cover.etaleAffineRefinement (ùí∞ : S.Cover (precoverage @Etale)) :
    S.AffineCover @Etale where
  I‚ÇÄ := (ùí∞.bind fun j ‚Ü¶ (ùí∞.X j).affineCover.changeProp (fun _ ‚Ü¶ inferInstance)).I‚ÇÄ
  X _ := _
  f := (ùí∞.bind fun j => (ùí∞.X j).affineCover.changeProp fun _ ‚Ü¶ inferInstance).f
  idx := Cover.idx (ùí∞.bind fun j => (ùí∞.X j).affineCover.changeProp fun _ ‚Ü¶ inferInstance)
  covers := Cover.covers (ùí∞.bind fun j => (ùí∞.X j).affineCover.changeProp fun _ ‚Ü¶ inferInstance)
  map_prop j := by
    simp [Cover.changeProp]
    have : Etale (ùí∞.f j.fst) := ùí∞.map_prop _
    infer_instance

def AffineEtale (S : Scheme.{u}) : Type (u + 1) :=
  MorphismProperty.CostructuredArrow @Etale.{u} ‚ä§ Scheme.Spec.{u} S

namespace AffineEtale

@[simps!]
protected def mk {R : CommRingCat} (f : Spec R ‚ü∂ S) [Etale f] : AffineEtale S :=
  MorphismProperty.CostructuredArrow.mk ‚ä§ f ‚Äπ_‚Ä∫

instance : Category S.AffineEtale :=
  inferInstanceAs <| Category (MorphismProperty.CostructuredArrow _ _ _ _)

@[simps! obj_left obj_hom map_left]
protected noncomputable def Spec (S : Scheme.{u}) : S.AffineEtale ‚•§ S.Etale :=
  MorphismProperty.CostructuredArrow.toOver _ _ _

instance : (AffineEtale.Spec S).Faithful :=
  inferInstanceAs <| (MorphismProperty.CostructuredArrow.toOver _ _ _).Faithful

instance : (AffineEtale.Spec S).Full :=
  inferInstanceAs <| (MorphismProperty.CostructuredArrow.toOver _ _ _).Full

instance : (AffineEtale.Spec S).IsCoverDense S.smallEtaleTopology :=
  inferInstanceAs <| (MorphismProperty.CostructuredArrow.toOver _ _ _).IsCoverDense
    (smallGrothendieckTopology _)

variable (S) in
noncomputable def topology : GrothendieckTopology S.AffineEtale :=
  (AffineEtale.Spec S).inducedTopology (smallEtaleTopology S)

/-- The category of sheafs on the small, affine √©tale site is equivalent to the category of
sheafs on the small √©tale site. -/
noncomputable def sheafEquiv (A : Type*) [Category A]
    [‚àÄ (X : S.Etale·µí·µñ), Limits.HasLimitsOfShape (StructuredArrow X (AffineEtale.Spec S).op) A] :
    Sheaf (AffineEtale.topology S) A ‚âå Sheaf (smallEtaleTopology S) A :=
  (AffineEtale.Spec S).sheafInducedTopologyEquivOfIsCoverDense _ _

instance : (AffineEtale.Spec S).ReflectsEffectiveEpis :=
  inferInstanceAs <| (MorphismProperty.CostructuredArrow.toOver _ _ _).ReflectsEffectiveEpis

instance effectiveEpi_of_surjective {S : Scheme} {X Y : S.AffineEtale} (f : X ‚ü∂ Y)
    [Surjective (Spec.map f.left.unop)] : EffectiveEpi f := by
  apply (AffineEtale.Spec S).effectiveEpi_of_map
  have : Surjective ((AffineEtale.Spec S).map f).left := ‚Äπ_‚Ä∫
  infer_instance

instance : HasPullbacks S.AffineEtale :=
  inferInstanceAs <| HasPullbacks (MorphismProperty.CostructuredArrow _ _ _ _)

-- Question: What are the effective epimorphisms of `AffineEtale S`?

instance preregular : Preregular (AffineEtale S) := by
  sorry

instance precoherent : Precoherent (AffineEtale S) :=
  sorry

end AffineEtale

end Scheme

end AlgebraicGeometry
